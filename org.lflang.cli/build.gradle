
sourceSets {
    test {
        java.srcDirs = []
        kotlin.srcDirs = ['test/kotlin']
        resources.srcDir('test/resources')
        resources.include '**/*'
    }
}

compileTestKotlin {
    destinationDir = compileTestJava.destinationDir
    kotlinOptions {
        jvmTarget = kotlinJvmTarget
    }
}



dependencies {
    implementation project(':org.lflang')
    implementation "org.eclipse.xtext:org.eclipse.xtext.ide:${xtextVersion}"
    implementation "org.eclipse.xtext:org.eclipse.xtext.xbase.ide:${xtextVersion}"

    testImplementation "junit:junit:${jUnitVersion}"
}

apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

task buildLfc() {
    apply plugin: 'application'
    apply plugin: 'com.github.johnrengelman.shadow'
    mainClassName = 'org.lflang.cli.Lfc'

    shadowJar {
        archiveClassifier.set('lfc')
        exclude 'test/*'
        minimize() {
            exclude(dependency('log4j:log4j:.*'))
            exclude(dependency('com.google.inject:guice:.*'))
            exclude(dependency('org.lflang:org.lflang:.*'))
        }
        transform(com.github.jengelman.gradle.plugins.shadow.transformers.AppendingTransformer){
            resource = 'plugin.properties'
        }
    }
}

buildLfc.finalizedBy shadowJar

task runLfc(type: JavaExec) {
    // builds and runs lfc
    // The working directory will be the root directory of the lingua franca project
    // CLI arguments can be passed to lfc by using --args. Note that you need
    // to escape cli flags which start with --.For instance --args ' --help'.
    // Otherwise they're parsed as arguments to the Gradle CLI, not lfc.
    description = "Build and run lfc, use --args to pass arguments"
    group = "application"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'org.lflang.cli.Lfc'
    workingDir = '..'
}
