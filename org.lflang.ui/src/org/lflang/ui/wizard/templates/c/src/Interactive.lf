/**
 * Simple demonstration of the sensor simulator (used in the Rhythm examples).
 * This has no audio output, but just tests the ncurses interface.
 */
target C {
    threads: 2,
    cmake-include: [
        "include/ncurses-cmake-extension.txt", // Adds support for ncurses
        "/lib/c/reactor-c/util/sensor_simulator.cmake"
        ],
    files: [
            "/lib/c/reactor-c/util/sensor_simulator.c", 
            "/lib/c/reactor-c/util/sensor_simulator.h"
        ]
};
preamble {=
    #include "sensor_simulator.h"
    char* messages[] = {"Hello", "World"};
    int num_messages = 2;
=}
main reactor {
    timer t(0, 1 sec);
    timer r(0, 2 sec);
    physical action key:char*;
    reaction(startup) -> key {=
        info_print("Starting sensor simulator.");
        start_sensor_simulator(messages, num_messages, 16, NULL, LOG_LEVEL_INFO);
        register_sensor_key('\0', key);
    =}
    reaction(t) {=
        show_tick("*");
    =}
    reaction(r) {=
        info_print("Elapsed logical time: %lld.", get_elapsed_logical_time());
        show_tick(".");
    =}
    reaction(key) {=
        info_print("You typed '%s' at elapsed time %lld.", key->value, get_elapsed_logical_time());
    =}
}
