buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.xtext:xtext-gradle-plugin:${xtextGradleVersion}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
    }
}

plugins {
    id "com.github.johnrengelman.shadow" version "${shadowJarVersion}"
    id 'java'
    id 'jacoco'
    id 'org.jetbrains.kotlin.jvm' version "${kotlinVersion}"
}

subprojects {
    repositories {
        mavenCentral()
    }
    
    apply plugin: 'kotlin'
    compileKotlin {
        destinationDir = compileJava.destinationDir
        kotlinOptions {
            jvmTarget = kotlinJvmTarget
        }
    }
    kotlinVersion = '1.5.31' // sync with version in plugin block above

    dependencies {
        implementation platform("org.eclipse.xtext:xtext-dev-bom:${xtextVersion}")
        // https://mvnrepository.com/artifact/com.google.inject/guice
        implementation group: 'com.google.inject', name: 'guice', version: guiceVersion
        // https://mvnrepository.com/artifact/commons-cli/commons-cli
        implementation group: 'commons-cli', name: 'commons-cli', version: commonsCliVersion
    }
    dependencies {
        implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: kotlinVersion
        implementation group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: kotlinVersion
    }


    apply plugin: 'org.xtext.xtend'
    apply from: "${rootDir}/gradle/source-layout.gradle"
    apply plugin: 'eclipse'

    // generate xtend sources before kotlin compilation
    compileKotlin.dependsOn("generateXtext")

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    configurations.all {
        exclude group: 'asm'
    }

    // Delete generated sources on `gradle clean`
    clean.doFirst {
        project.logger.info("Deleting ${projectDir}/xtend-gen, src-gen")
        delete "$projectDir}/xtend-gen/"
        delete "${projectDir}/src-gen/"
    }
}

gradle.projectsEvaluated {
    // Our CI uses --tests filters, which fails if some
    // subprojects have no matching test.
    //
    // https://stackoverflow.com/questions/26147480/how-to-make-gradle-not-to-mark-build-failed-if-no-tests-are-found
    subprojects {
        test {
            filter {
                setFailOnNoMatchingTests(false)
            }
        }
    }
}
