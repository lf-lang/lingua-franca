/**
 * Micro-benchmark from the Savina benchmark suite, intended
 * to measure message passing overhead and switching between
 * actors.
 * See https://shamsimam.github.io/papers/2014-agere-savina.pdf.
 * 
 * To open the causality loop in the ring of reactors one
 * reactor uses a logical action for message passing.
 * 
 * @author Matthew Chorlian
 */

/* [[[cog
# This file is a code generator using the python module cog:
# See https://nedbatchelder.com/code/cog/
#
# All instructions for code  generation are in-lined in comments
# like this one. With that you can use this file as a normal source file
# but also to generate code.
# 
# To change the generated code in-line within this file run:
# $ python -m cog -r this-file.lf
# To generate a new file from this file stripping the generator code in the process run:
# $ python -m cog -d -o output-file.lf this-file.lf
#
# Use the command line option -D to specify generator parameters, for example:
# $ python -m cog -r -D parameter=100 this-file.lf
#
# Generator parameters used in this file:
# -D numReactors=100
# -D numPings = 100000
]]] */
// [[[end]]]

/* [[[cog
  # force existence, type and default values of generator parameters
  if 'numReactors' in globals():
    numReactors = int(numReactors)
  else:
    globals()['numReactors'] = 100
  
  # output the current value of the generator parameters used in the last generation run
  cog.outl(f'// Generated file with the following parameters:')
  cog.outl(f'// numReactors = {numReactors}')
  cog.outl(f'// numPings = {numPings}')


]]] */
// Generated file with the following parameters:
// numReactors = 100
// numPings = 100000
// [[[end]]]

target C {
    /* [[[cog
      if (threaded_runtime=="True"):
          cog.outl(f"threads: {threads},")
      else:
          cog.outl("threads: 0,")
    ]]] */
    threads: 0,
    /// [[[end]]]
};

preamble {=
    int ping;

    bool hasNext(int ping) {
        if (ping > 0) {
            return true;
        } else {
            return false;
        }
    }
    
    int getPingsLeft(int ping) {
        return ping;
    }
    
    int ping_next(int ping) {
        return ping - 1;
    }
=}

reactor ThreadRingReactor {
    
    
    output outNextReactor:int;
    input inPrevReactor:int;
    input start:int;
    output finished:bool;
    
    reaction(inPrevReactor) -> outNextReactor, finished {=
        if (hasNext(inPrevReactor->value)) {
            SET(outNextReactor, ping_next(inPrevReactor->value));
        } else {
            SET(finished, true);
            printf("Finished with count %d, ", getPingsLeft(inPrevReactor->value));
        }
    =}
    
    reaction(start) -> outNextReactor, finished {=
        if (hasNext(start->value)) {
            SET(outNextReactor, ping_next(start->value));
            printf("Starting with count %d", getPingsLeft(start->value));
        } else {
            SET(finished, true);
        }
    =}
}

reactor ThreadRingReactorLoopOpener {
    
    output outNextReactor:int;
    input inPrevReactor:int;
    input start:int;
    output finished:bool;
    
    logical action sendToNextReactor:int;
    
    // this is where the loop terminates as of right now
    reaction(sendToNextReactor) -> outNextReactor {=
        SET(outNextReactor, sendToNextReactor->value);
    =}
    
    reaction(inPrevReactor) -> sendToNextReactor, finished {=
        if (hasNext(inPrevReactor->value)) {
            sendToNextReactor->value = ping_next(inPrevReactor->value);
            schedule(sendToNextReactor, 0);
        } else {
            SET(finished, true);
            printf("Finished with count %d",  getPingsLeft(inPrevReactor->value));
        }
    =}
    
    reaction(start) -> sendToNextReactor, finished {=
        printf("Starting with count %d, ", getPingsLeft(start->value));
        if (hasNext(start->value)) {
            sendToNextReactor->value = ping_next(start->value);
            schedule(sendToNextReactor, 0);
        } else {
            SET(finished, true);
        }
    =}
}

reactor Initializer(numReactors:int(100), numPings:int(100000)) {
    
    input inStart:bool;
    output outFinished:bool;
    
    
    input[numReactors] inFinished:bool;
    output outStart:int;
    
    reaction(inStart) -> outStart {=
        SET(outStart, self->numPings);
    =}
    
    reaction(inFinished) -> outFinished {=
        SET(outFinished, true);
    =}
}

/*[[[cog
cog.outl(f'main reactor (numPings:int({numPings}), numReactors:int({numReactors}))')
]]] */
main reactor (numPings:int(100000), numReactors:int(100)) 

/// [[[end]]]

{
    
    init = new Initializer(numReactors=numReactors, numPings=numPings);    
    
    reaction(startup) -> init.inStart {=
        SET(init.inStart, true);
    =}
    
    
    loopOpener = new ThreadRingReactorLoopOpener();

    /*[[[cog
    cog.outl(f'workers = new[{numReactors - 1}] ThreadRingReactor();')
    ]]] */
    workers = new[99] ThreadRingReactor();
    /// [[[end]]]

    loopOpener.outNextReactor, workers.outNextReactor -> workers.inPrevReactor, loopOpener.inPrevReactor;
    
    init.outStart -> loopOpener.start;
    loopOpener.finished, workers.finished -> init.inFinished;
    
}
