/*
 * Concurrency benchmark from the Savina benchmark suite.
 * See https://shamsimam.github.io/papers/2014-agere-savina.pdf.
 * @author Hannes Klein
 * @author Marten Lohstroh
 * @author Soroush Bateni
 */
 
target C {
    /* [[[cog
      if (threaded_runtime=="True"):
          cog.outl(f"threads: {threads},")
      else:
          cog.outl("threads: 0,")
    ]]] */
    threads: 0,
    /// [[[end]]]
    flags: "-lm",
    build-type: Debug  
};


preamble {=
    #include <stdlib.h>
    #include <stddef.h>
    
    typedef double message_content_t;
    
    typedef enum MsgType {
      DataItemMsg,
      ProduceDataMsg,
      ProducerExitMsg,
      ConsumerAvailableMsg,
      ConsumerExitMsg
    } msg_type_t;
    
    typedef struct Message {
      msg_type_t type;
      message_content_t data;
    } message_t;
    
    typedef struct bounded_fifo_t {
        size_t head;
        size_t tail;
        size_t max_size;
        bool is_full;
        void** arr;
    } bounded_fifo_t;
    
    /**
     * Report whether the given queue is empty.
     */
    bool is_empty(bounded_fifo_t* queue) {
        if (queue->head == queue->tail && !queue->is_full) {
            return true;
        } else {
            return false;
        }
    }
    
    /**
     * Report how many elements are in the queue.
     */
    size_t size(bounded_fifo_t* queue) {
        if (is_empty(queue)) {
            return 0;
        } else if (queue->is_full) {
            return queue->max_size;
        } else if (queue->tail > queue->head) {
            return queue->tail - queue->head;
        } else {
            return queue->max_size - (queue->head - queue->tail);
        }
    }

    /**
     * Clear the given queue by resetting its head and tail pointer.
     */    
    void clear(bounded_fifo_t* queue) {
        queue->head = 0;
        queue->tail = 0;
        queue->is_full = false;
    }
    
    /**
     * Create a new bounded FIFO queue.
     */
    bounded_fifo_t* create_fifo(size_t capacity) {
        bounded_fifo_t* queue = malloc(sizeof(bounded_fifo_t));
        queue->arr = (void**)malloc(capacity * sizeof(void*));
        queue->max_size = capacity;
        clear(queue);
        return queue;
    }
    
    /**
     * Assign the given pointer to the first element in the queue.
     * Return true if the assignment was successful, false otherwise.
     */
    void* pop(bounded_fifo_t* queue) {
        if (is_empty(queue)) {
            printf("Buffer is empty.\n");
            return NULL;
        } else {
            int old_head = queue->head;
            queue->head = (queue->head + 1) % queue->max_size;
            queue->is_full = false;
        	return queue->arr[old_head];
        }
    }
    
    /**
     * Add a new entry to the queue.
     */
    bool push(bounded_fifo_t* queue, void* elem) {
        if (queue->is_full) {
        	printf("Buffer is full.\n");
			return false;        	
        } else {
            queue->arr[queue->tail] = elem;
            queue->tail = (queue->tail + 1) % queue->max_size;
            if (queue->head == queue->tail) {
            	queue->is_full = true;
            }
        	return true;
        }
    }
    
        #include <math.h>
        
        static double processItem(const double curTerm, const int cost) {
            
            double res = curTerm;
            
            srand(cost);
            if(cost > 0) {
                for(int i = 0; i < cost; i++) {
                    for(int j = 0; j < 100; j++) {
                        res += log(abs(rand()) + 0.01);
                    }
                }
            } else {
                res += log(abs(rand()) + 0.01);
            }
            
            return res;
        }
    
=}

reactor ManagerReactor(bufferSize:size_t(50), numProducers:size_t(40), numConsumers:size_t(40)) {
    
    state adjustedBufferSize:int(0);
    state availableProducers:bounded_fifo_t*;
    state availableConsumers:bounded_fifo_t*;
    state pendingData:bounded_fifo_t*;
    state numTerminatedProducers:int(0);
    
    input inStart:bool;
    output outFinished:bool;
    
    logical action consume;
    logical action produce;
    
    input[numProducers] inProducers:message_t;
    output[numProducers] outProducers:message_t;
    input[numConsumers] inConsumers:message_t;
    output[numConsumers] outConsumers:message_t;
    
    reaction(startup) {=
        // init local state
        self->adjustedBufferSize = self->bufferSize-self->numProducers;
        self->availableProducers = create_fifo(self->numProducers);
        self->availableConsumers = create_fifo(self->numConsumers);
        self->pendingData = create_fifo(self->bufferSize);
    =}
    
    reaction(produce) -> outProducers {=
        while(size(self->pendingData) < self->adjustedBufferSize) { // FIXME: Why?            
            if(is_empty(self->availableProducers)) {
                printf("Ran out of available producers.\n");
                break;
            }
            int* idx;
            idx = pop(self->availableProducers);
            message_t tmp = {ProduceDataMsg, *idx + 1.0};
            SET(outProducers[*idx], tmp);
            printf("outProducers[%d] = %f.\n", *idx, outProducers[*idx]->value.data);
            free(idx);
        }
    =}
    
    reaction(consume) -> outConsumers, outProducers {=
        
        if(self->numTerminatedProducers == self->numProducers &&
            size(self->availableConsumers) == self->numConsumers) {
            return;
        }
        
        for (int i = 0; i < size(self->availableConsumers); i++) {
            if (is_empty(self->pendingData)) {
                break;
            }
            message_t* msg;
            msg = pop(self->pendingData); // FIXME: wrong data type
            printf("Popped %f.\n", msg->data);
            int* idx;
            idx = pop(self->availableConsumers);
            SET(outConsumers[*idx], *msg);
            free(idx);
            //free(msg);
            if (!is_empty(self->availableProducers)) {
                int* idx;
                idx = pop(self->availableProducers);
                printf("Popped %d.\n", *idx);
                message_t msg = {ProduceDataMsg, *idx + 1.0};
                SET(outProducers[*idx], msg);
                printf("outProducers[%d] = %f.\n", *idx, outProducers[*idx]->value.data);
                free(idx);
            }
        }
    =}
    
    reaction(inStart) -> produce {=
        // reset local state
        clear(self->availableProducers);
        clear(self->availableConsumers);
        clear(self->pendingData);
        self->numTerminatedProducers = 0;
        
        // start execution
        for(int i = 0; i < self->numProducers; i++) {
			int* producer = (int*)malloc(sizeof(int)); // FIXME: this should be freed
			*producer = i;
            push(self->availableProducers, producer);
        }
        for(int i = 0; i < self->numConsumers; i++) {
			int* consumer = (int*)malloc(sizeof(int)); // FIXME: this should be freed
			*consumer = i;
            push(self->availableConsumers, consumer);
        }
        schedule(produce, 0);
    =}
    
    reaction(inConsumers) -> consume {=
        for (int i = 0; i < inConsumers_width; i++) {
            if (inConsumers[i]->is_present) {
                if(inConsumers[i]->value.type == ConsumerAvailableMsg) {
					int* inConsumer = (int*)malloc(sizeof(int)); // FIXME: this should be freed
					*inConsumer = i;
                    push(self->availableConsumers, inConsumer);
                }
            }
        }
        schedule(consume, 0);
    =}
    
    reaction(inProducers) -> produce, consume, outFinished {=
        for(int i = 0; i < inProducers_width; i++) {
            if(inProducers[i]->is_present) {
                if (inProducers[i]->value.type == DataItemMsg) {
                    push(self->pendingData, &inProducers[i]->value); // FIXME: Not the right data type
					int* producer = (int*)malloc(sizeof(int)); // FIXME: this should be freed
					*producer = i;                    
                    push(self->availableProducers, producer);
                    schedule(consume, 0);
                    schedule(produce, 0);
                } else if(inProducers[i]->value.type == ProducerExitMsg) {
                    self->numTerminatedProducers += 1;
                    if(self->numTerminatedProducers == self->numProducers &&
                        size(self->availableConsumers) == self->numConsumers) {
                        SET(outFinished, true);
                        return;
                    }
                }
            }
        }
    =}
}

reactor ProducerReactor(numItemsToProduce:int(1000), prodCost:int(25)) {
        
    state prodItem:message_content_t(0.0);
    state itemsProduced:int(0);
    
    input inManager:message_t;
    output outManager:message_t;
    
    reaction(inManager) -> outManager {=
        if (inManager->value.type == ProduceDataMsg) {
            if(self->itemsProduced == self->numItemsToProduce) {
                message_t tmp = {ProducerExitMsg, 0.0};
                SET(outManager, tmp);
                
                // reset local state
                self->prodItem = 0.0;
                self->itemsProduced = 0;
                return;
            }
            message_t tmp = {DataItemMsg, processItem(self->prodItem, self->prodCost)};
            SET(outManager, tmp);
            self->itemsProduced += 1;
        }
    =}
    
}

reactor ConsumerReactor(consCost:int(25)) {
    
    preamble {=
        //#include <cmath>
        //#include "PseudoRandom.hh"
    =}
    
    state consItem:message_content_t(0.0);
    
    input inManager:message_t;
    output outManager:message_t;
    
    reaction(inManager) -> outManager {=        
        if (inManager->value.type == DataItemMsg) {
            message_content_t dataToConsume =  inManager->value.data;
            self->consItem = processItem(self->consItem + dataToConsume, self->consCost);
            message_t tmp = {ConsumerAvailableMsg, 0.0};
            SET(outManager, tmp);
        } else if(inManager->value.type == ConsumerExitMsg) {
            // reset local state
            self->consItem = 0.0;
        }
    =}
    
    }

//main reactor Test {
//	state q:bounded_fifo_t*;
//	reaction(startup) {=
//    	self->q = create_fifo(4);
//    	push(self->q, 1);
//		push(self->q, 2);
//		push(self->q, 3);
//		push(self->q, 4);
//		while(!is_empty(self->q)) {
//			int data;
//			if (pop(self->q, &data)) {
//            	printf("value: %d\n", data);
//				printf("size: %d\n", size(self->q));
//            }
//        }
//    =} 
//
//}

/* [[[cog
    cog.outl(f'main reactor ProdCons(bufferSize:int({bufferSize}), prodCost:int({prodCost}), consCost:int({consCost}), numItemsPerProducer:int({numItemsPerProducer}), numProducers:int({numProducers}), numConsumers:int({numConsumers}))')
]]] */
main reactor ProdCons(bufferSize:int(50), prodCost:int(25), consCost:int(25), numItemsPerProducer:int(1000), numProducers:int(40), numConsumers:int(40))
// [[[end]]]    
{
    manager = new ManagerReactor(bufferSize=bufferSize, numProducers=numProducers, numConsumers=numConsumers);
        
    
    
    reaction(startup) -> manager.inStart {=
        // FIXME: what is printArgs?
        // printArgs("numIterations", self->numIterations, "bufferSize", self->bufferSize, "prodCost", self->prodCost, "consCost", self>consCost, "numItemsPerProducer", self->numItemsPerProducer, "numProducers", 40, "numConsumers", 40);
        printf("bufferSize: %d.\n", self->bufferSize);
        printf("prodCost: %d.\n", self->prodCost);
        printf("consCost: %d.\n", self->consCost);
        printf("numItemsPerProducer: %d.\n", self->numItemsPerProducer);
        printf("numProducers: %d.\n", 40);
        printf("numConsumers: %d.\n", 40);
		
        SET(manager.inStart, true);
    =}
    
    producers = new[numProducers] ProducerReactor(numItemsToProduce=numItemsPerProducer, prodCost=prodCost);
    consumers = new[numConsumers] ConsumerReactor(consCost=consCost);
 
    
    
    manager.outProducers -> producers.inManager;
    producers.outManager -> manager.inProducers;
    
    manager.outConsumers -> consumers.inManager;
    consumers.outManager -> manager.inConsumers;
    
}
