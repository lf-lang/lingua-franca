dependencies {
    implementation project(':org.lflang')
    testImplementation "org.junit.jupiter:junit-jupiter-api:${jupiterVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:${jupiterVersion}"
    testImplementation "org.junit.platform:junit-platform-commons:${jUnitPlatformVersion}"
    testImplementation "org.junit.platform:junit-platform-engine:${jUnitPlatformVersion}"
    testImplementation "org.opentest4j:opentest4j:${openTest4jVersion}"
    testImplementation "org.eclipse.xtext:org.eclipse.xtext.testing:${xtextVersion}"
    testImplementation "org.eclipse.xtext:org.eclipse.xtext.xbase.testing:${xtextVersion}"
}

apply plugin: 'java'
apply plugin: 'jacoco'

jacoco {
    toolVersion = jacocoVersion
    reportsDir = file("$buildDir/reports/jacoco") //default directory where jacoco generates test reports
}


jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled true
        html.destination file("${buildDir}/reports/html/jacoco")
        xml.destination file("${buildDir}/reports/xml/jacoco")
        csv.destination file("${buildDir}/reports/csv/jacoco")
    }
    def fileFilter = [  'org/lflang/services/**', 
                        'org/lflang/linguaFranca/impl/**', 
                        'org/lflang/serializer/**', 
                        'org/lflang/linguaFranca/util/**', 
                        'org/lflang/linguaFranca/**',
                        'org/lflang/parser/antlr/**'
                    ]
    def mainCls = fileTree(dir: "$project.buildDir/../../org.lflang/build/classes/java/main", excludes: fileFilter)
    def javaSrc = "$project.buildDir/../../org.lflang/src"
    
    classDirectories.from = files(mainCls)
    sourceDirectories.from = files([javaSrc])
}

test {
    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
    }
    // Pass the scheduler property on to the Java VM
    systemProperty 'scheduler', System.getProperty('scheduler')
    systemProperty 'runtime', System.getProperty('runtime')
    // Suggested by Gradle documentation: https://guides.gradle.org/performance/#parallel_test_execution
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
	useJUnitPlatform()
    finalizedBy jacocoTestReport
    workingDir = ".."
    filter {
        setFailOnNoMatchingTests(true)
    }
}

test.dependsOn("compileKotlin")

task runSingleTest(type: JavaExec) {
    description = "Execute a single test, use with eg --args test/Python/src/Minimal.lf"
    group = "test"
    classpath = sourceSets.test.runtimeClasspath
    workingDir = ".."
    mainClass = 'org.lflang.tests.RunSingleTestMain'
}
