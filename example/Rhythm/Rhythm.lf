/**
 * Simple demonstration of asynchronous input and timed output.
 * This program opens a simple, terminal-based user interface for
 * specifying a rhythmic audio output. The rhythm is displayed in
 * the terminal as it is generated and produced as audio using
 * the terminal's "beep" functionality. Note that not all terminal
 * implementation provide audio output, so you may not hear any
 * audible beeps. Moreover, many terminal implementations implement
 * the beep in such a way that it occupies a fixed amount of time
 * that may be longer than the 'sixteenth' parameter given here,
 * in which case the audible rhythm will not be accurate.
 * Regrettably, this "beep" functionality is the closest we have
 * been able to find to any sort of even remotely portable audio
 * output.
 * 
 * This program uses ncurses, which needs to be installed on your machine
 * for this to work. It also uses the library utility sensor_simulator,
 * provided with Lingua Franca, which uses keyboard input to simulate
 * asynchronous sensors and beeps to simulate timed output.
 * See lingua-franca/lib/C/sensor_simulator.h.
 * 
 * You can construct simple rhythms with this program.
 * The sixteenth parameter determines the duration of a sixteenth note,
 * the finest resolution this example supports.
 * You can turn on and off several beeps to occur within the bar
 * with keyboard input.
 */
target C {
    flags: "-lncurses",
    files: ["/lib/C/util/sensor_simulator.c", "/lib/C/util/sensor_simulator.h"] 
};
preamble {=
    #include <ncurses.h>
    #include <unistd.h>
    #include "sensor_simulator.c"
    
    #define CLEAR 0x0000
    #define DOWNBEAT 0x01
    #define SIXTEENTHNOTE 0x02
    #define EIGHTHNOTE 0x04
    #define QUARTERNOTE 0x10
    #define HALFNOTE 0x100
    #define UPBEAT 0x1000
    
    // Merenque in binary covers two bars (in temporal order):  1001 0101 0101 1111
    // Reverse the order to get the bit sequence: 1111 0101 0101 1001
    #define MERENGUE 0xf559
    
    
    char* message[] = {
        "Type the following keys:",
        "  a: start and stop all sound",
        "  x: quit",
        "  d: down beat",
        "  s: sixteenth note",
        "  e: eighth note",
        "  q: quarter note",
        "  h: half note",
        "  u: up beat",
        "  m: merengue",
        "  c: clear"
     };
=}

main reactor Rhythm(sixteenth:time(600 msec)) {
    timer t(0, sixteenth);
    
    // Count of sixteenth notes.
    state count:int(0);
    
    // Indicator of whether any sound is produced.
    state sound:bool(false);
    
    // Action to be invoked when a key is pressed.
    physical action key:char;
    
    // Indicator of when to make a sound.
    state rhythm:int(0);
    
    // Currently active rhythm.
    state active_rhythm:int(0);
        
    // Position of the cursor in the terminal window.
    state cursor:int(0);
    
    reaction(startup) -> key {=
        // Start the sensor simulator, which starts ncurses.
        if (start_sensor_simulator()) {
            fprintf(stderr, "ERROR: Failed to start sensor simulator.\n");
            exit(1);
        }
        // Print sound is off message.
        int term_height, term_width;
        getmaxyx(stdscr, term_height, term_width);   // Get the size of the terminal window.
        mvprintw(0, (term_width - 12)/2, "SOUND IS OFF");
        refresh();

        show_message(message, 11);
        
        // Register action to trigger on key press.
        register_sensor_key('\0', key);
    =}
    
    reaction(key) {=
        switch (key->value) {
            case 'a':
                self->sound = !self->sound;
                // Ensure there is a downbeat, at least.
                if (self->rhythm == 0) {
                    self->rhythm = self->rhythm | DOWNBEAT;
                }
                // Update the status message at the top.
                // First, record current cursor position.
                int y, x;
                getyx(stdscr, y, x);
                // Next, get window dimensions.
                int term_height, term_width;
                getmaxyx(stdscr, term_height, term_width);
                // Next, print.
                if (self->sound) {
                    mvprintw(0, (term_width - 12)/2, "SOUND IS ON ");
                } else {
                    mvprintw(0, (term_width - 12)/2, "SOUND IS OFF");
                }
                refresh();
                // Finally, restore cursor position.
                move(y, x);
                break;
            case 'd':
                self->rhythm = self->rhythm ^ DOWNBEAT;
                break;
            case 's':
                self->rhythm = self->rhythm ^ SIXTEENTHNOTE;
                break;
            case 'e':
                self->rhythm = self->rhythm ^ EIGHTHNOTE;
                break;
            case 'q':
                self->rhythm = self->rhythm ^ QUARTERNOTE;
                break;
            case 'h':
                self->rhythm = self->rhythm ^ HALFNOTE;
                break;
            case 'u':
                self->rhythm = self->rhythm ^ UPBEAT;
                break;
            case 'm':
                self->rhythm =  MERENGUE;
                break;
            case 'c':
                self->rhythm =  CLEAR;
                break;
            case 'x':
                request_stop();
                break;
        }
    =}

    reaction(t) {=
        int beeped = 0;
        if (self->sound) {
            int position = 1 << self->count;
            if (position & self->active_rhythm) {
                beep();
                beeped++;
            }
        }
        pthread_mutex_lock(&sensor_mutex);
        if (beeped > 0) {
            printw("*");
        } else {
            printw(".");
        }
        self->count++;
        if (self->count == 16) {
            self->active_rhythm = self->rhythm;
            self->count = 0;
            int term_height, term_width;
            getmaxyx(stdscr, term_height, term_width);   // Get the size of the terminal window.
            self->cursor++;
            if (self->cursor >= term_height) {
                self->cursor = 0;
            }
            move(self->cursor, 0);
        }
        refresh();
        pthread_mutex_unlock(&sensor_mutex);
    =}
} 
