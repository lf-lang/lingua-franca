/**
 * Distributed LF program where a MessageGenerator creates a string
 * message that is sent via the RTI (runtime infrastructure) to a
 * receiver that prints the message.
 * 
 * The code generator generates three programs:
 * 
 * * bin/DistributedHelloWorld: A script that launches the other three
 *   programs on localhost.  Run this program.
 *  
 * * bin/DistrubtedHelloWorld_source: The program that produces the sequence
 *   of messages.
 * 
 * * bin/DistrubtedHelloWorld_print: The program that produces the sequence
 *   of messages.
 * 
 * To run this manually, you can start the RTI and then each of the last 
 * two programs. They will synchronize the start time and run for 10 seconds.
 * 
 * Note: The RTI is a separate program that has to be installed separately.
 *  See https://github.com/lf-lang/reactor-c/tree/main/core/federated/RTI
 * 
 * @author Edward A. Lee
 */
target C {
    timeout: 10 secs
};

/**
 * Reactor that generates a sequence of messages, one per second.
 * The message will be a string consisting of a prefix string followed
 * by a count.
 * @param prefix The prefix string.
 * @output message The message.
 */
reactor MessageGenerator(prefix:string("")) {
    // Output type char* instead of string is used for dynamically
    // allocated character arrays (as opposed to static constant strings).
    output message:char*;
    state count:int(1);
    // Send first message after 1 sec so that the startup reactions
    // do not factor into the transport time measurement on the first message.
    timer t(1 sec, 1 sec);
    reaction(t) -> message {=
        // With NULL, 0 arguments, snprintf tells us how many bytes are needed.
        // Add one for the null terminator.
        int length = snprintf(NULL, 0, "%s %d", self->prefix, self->count) + 1;
        // Dynamically allocate memory for the output.
        SET_NEW_ARRAY(message, length);
        // Populate the output string and increment the count.
        snprintf(message->value, length, "%s %d", self->prefix, self->count++);

        tag_t tag = get_current_tag();
        info_print("At (elapsed) logical tag (%lld, %u), source sends message: %s",
            tag.time - start_time, tag.microstep,
            message->value
        );
    =}
}

/**
 * Reactor that prints the current tag and an incoming string.
 *
 * @input message The message.
 */
reactor PrintMessage {
    input message:char*;
    reaction(message) {=
        tag_t tag = get_current_tag();
        info_print("At (elapsed) logical tag (%lld, %u), print receives: %s",
            tag.time - start_time, tag.microstep,
            message->value
        );
    =}
}

federated reactor HelloWorld {
    source = new MessageGenerator(prefix = "Hello World");
    print = new PrintMessage();
    source.message -> print.message;
}