// This is a variant of the example is considered in this paper:
//   https://www.mdpi.com/2227-7390/8/7/1068
// where it is studied for its verifiability.
target C;
reactor Controller {
  output close:bool; output lock:bool;
  output unlock:bool; output open:bool;
  physical action external:bool;
  reaction(startup) {=
    // ... Set up external sensing.
  =}
  reaction(external)->close, lock, open, unlock {=
    if (external->value) {
      SET(close, true); SET(lock, true);
    } else {
      SET(open, true); SET(unlock, true);
    }
  =}
}
reactor Door {
  input close:bool; input lock:bool;
  input unlock:bool; input open:bool;
  physical action ext_open:bool;
  state locked:bool(false);
  state is_open:bool(false);
  reaction(close) {=
    // ... Actuate to close door.
    self->is_open = false;
  =}
  reaction(lock) {=
    // ... Actuate to lock door.
    if(!self->is_open) 
       self->locked = true;
  =}
  reaction(unlock) {=
    // ... Actuate to unlock door.
    self->locked = false;
  =}
  reaction(open, ext_open) {=
    // ... Actuate to open door.
    if(!self->locked) 
       self->is_open = true;
  =}
}
main reactor System {
  c = new Controller(); 
  d = new Door();
  c.lock -> d.lock after 5 msec; // |\label{line:unlockafter}|
  c.unlock -> d.unlock after 4 msec; // |\label{line:unlockafter}|
  c.open -> d.open after 7 msec; // |\label{line:unlockafter}|
  c.close -> d.close after 3 msec; // |\label{line:unlockafter}|
}
