// This is a variant of the example is considered in this paper:
//   https://www.mdpi.com/2227-7390/8/7/1068
// where it is studied for its verifiability.
target C;
reactor Controller {
  output lock:bool;
  output move:bool;
  physical action external_move:bool;
  reaction(startup) {=
    // ... Set up sensing.
  =}
  reaction(external_move)->lock, move {=
    SET(lock, external_move->value);
    SET(move, external_move->value);
  =}
}
reactor Train {
  input move:bool;
  state moving:bool(false);
  reaction(move) {=
    if (move) {
      self->moving = true;
    } else {
      self->moving = false;
    }
  =}
}
reactor Door {
  input lock:bool;
  physical action external_open;
  state locked:bool(false);
  state open:bool(false);
  reaction(startup) {=
    // ... Set up sensing.
  =}
  reaction(lock) {=
    if (lock && self->open) {
      // ... Actuate to close door.
      self->open = false;
    }
    self->locked = lock->value;
  =}
  reaction(external_open) {=
    if (!self->locked) {
      // ... Actuate to open door.
      self->open = true;
    }
  =}
}
main reactor System {
  controller = new Controller();
  door = new Door();
  train = new Train();
  controller.lock -> door.lock;
  controller.move -> train.move;
}
