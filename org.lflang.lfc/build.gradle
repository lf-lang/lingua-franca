dependencies {
    implementation project(':org.lflang')
    implementation "org.eclipse.xtext:org.eclipse.xtext.ide:${xtextVersion}"
    implementation "org.eclipse.xtext:org.eclipse.xtext.xbase.ide:${xtextVersion}"
}

apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

task generateStandaloneCompiler() {
    apply plugin: 'application'
    apply plugin: 'com.github.johnrengelman.shadow'
    mainClassName = 'org.lflang.lfc.Main'

    shadowJar {
        exclude 'test/*'
        minimize() {
            exclude(dependency('log4j:log4j:.*'))
            exclude(dependency('com.google.inject:guice:.*'))
            exclude(dependency('org.lflang:org.lflang:.*'))
        }
        transform(com.github.jengelman.gradle.plugins.shadow.transformers.AppendingTransformer){
            resource = 'plugin.properties'
        }
    }
}

generateStandaloneCompiler.finalizedBy shadowJar

task runLfc(type: JavaExec) {
    // Note: when you use --args, you need to escape cli flags which start with --
    // For instance --args ' --help'
    // Otherwise they're parsed as arguments to the Gradle CLI, not LFC.
    description = "Build and run LFC, use --args to pass arguments"
    group = "application"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'org.lflang.generator.Main'
}