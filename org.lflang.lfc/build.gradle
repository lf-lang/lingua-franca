
sourceSets {
    test {
        java.srcDirs = []
        kotlin.srcDirs = ['test/kotlin']
        resources.srcDir('test/resources')
        resources.include '**/*'
    }
}

compileTestKotlin {
    destinationDir = compileTestJava.destinationDir
    kotlinOptions {
        jvmTarget = kotlinJvmTarget
    }
}



dependencies {
    implementation project(':org.lflang')
    implementation "org.eclipse.xtext:org.eclipse.xtext.ide:${xtextVersion}"
    implementation "org.eclipse.xtext:org.eclipse.xtext.xbase.ide:${xtextVersion}"

    testImplementation "junit:junit:${jUnitVersion}"
}

apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

task buildLfc() {
    apply plugin: 'application'
    apply plugin: 'com.github.johnrengelman.shadow'
    mainClassName = 'org.lflang.lfc.Main'

    shadowJar {
        exclude 'test/*'
        transform(com.github.jengelman.gradle.plugins.shadow.transformers.AppendingTransformer){
            resource = 'plugin.properties'
        }
        // We should use minimize() here to reduce the size of the JAR, but it causes problems
        // with regard to our Kotlin classes. Since we don't use imports to load them but load
        // the classes manually, minimize does not see the dependency. While we can add an exclude
        // rule, this does not seem to work very well and causes problems when compiling for a
        // second time. Also see https://github.com/lf-lang/lingua-franca/pull/1285
    }
}

buildLfc.finalizedBy shadowJar

task runLfc(type: JavaExec) {
    // builds and runs lfc
    // The working directory will be the root directory of the lingua franca project
    // CLI arguments can be passed to lfc by using --args. Note that you need
    // to escape cli flags which start with --.For instance --args ' --help'.
    // Otherwise they're parsed as arguments to the Gradle CLI, not lfc.
    description = "Build and run lfc, use --args to pass arguments"
    group = "application"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'org.lflang.lfc.Main'
    workingDir = '..'
}
