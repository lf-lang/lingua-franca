// This is a version of the Threaded test that uses a multiport input at the destination. Its
// purpose is to test multiport inputs.
target TypeScript {
  timeout: 2 sec
}

reactor Source {
  timer t(0, 200 msec)
  output out: number
  state s: number = 0

  reaction(t) -> out {=
    out = s;
    s++;
  =}
}

reactor Computation {
  input inp: number
  output out: number

  reaction(inp) -> out {= out = inp; =}
}

reactor Destination(width: number = 1) {
  state s: number = 0
  input[width] inp: number

  reaction(inp) {=
    let sum = 0;
    for (let i = 0; i < inp.length; i++) {
      let val = inp[i];
      if (val !== undefined) {
        sum += val;
      }
    }
    console.log("Sum of received: " + sum + ".");
    if (sum != s) {
      util.requestErrorStop("ERROR: Expected " + s + ".");
    }
    s += 4;
  =}

  reaction(shutdown) {=
    if (s == 0) {
      util.reportError("ERROR: Destination received no input!");
    }
    console.log("Success.");
  =}
}

main reactor {
  a = new Source()
  t1 = new Computation()
  t2 = new Computation()
  t3 = new Computation()
  t4 = new Computation()
  b = new Destination(width=4)
  a.out -> t1.inp
  a.out -> t2.inp
  a.out -> t3.inp
  a.out -> t4.inp
  // I.e.: t1.out -> b.inp[0]; t2.out -> b.inp[1]; t3.out -> b.inp[2]; dt4.out
  // -> b.inp[3];
  t1.out, t2.out, t3.out, t4.out -> b.inp
}
