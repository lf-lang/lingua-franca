// Check multiport output to multiport input, where the latter is a hierarchical reactor. Note that
// the destination reactor has width wider than the sender, so one input is dangling.
target TypeScript {
  timeout: 2 sec
}

reactor Source(width: number = 4) {
  timer t(0, 200 msec)
  output[width] out: number
  state s: number = 0

  reaction(t) -> out {=
    for(let i = 0; i < 4; i++) {
      out[i] = s++;
    }
  =}
}

reactor Destination(width: number = 4) {
  state s: number = 6
  input[width] inp: number

  reaction(inp) {=
    let sum = 0;
    for (let i = 0; i < inp.length; i++) {
      const val = inp[i]
      if (val !== undefined) sum += val;
    }
    console.log("Sum of received: " + sum + ".");
    if (sum != s) {
      util.requestErrorStop("ERROR: Expected " + s + ".");
    }
    s += 16;
  =}

  reaction(shutdown) {=
    if (s <= 6) {
      util.requestErrorStop("ERROR: Destination received no input!");
    }
    console.log("Success.");
  =}
}

reactor Container(width: number = 4) {
  input[width] inp: number
  dst = new Destination()
  inp -> dst.inp
}

main reactor MultiportToHierarchy(width: number = 4) {
  a = new Source(width=width)
  b = new Container(width=width)
  a.out -> b.inp
}
