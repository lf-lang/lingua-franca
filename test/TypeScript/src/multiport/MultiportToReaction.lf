// Check reaction to multiport output of a contained reactor.
target TypeScript {
    timeout: 2 sec
}; 
reactor Source(width:number(1)) {
    timer t(0, 200 msec);
    state s:number(0);
    output[width] out:number;
    reaction(t) -> out {=
        console.log("Sending.");
        for (let i = 0; i < out.length; i++) {
            out[i] = s++;
        }
    =}
}
main reactor MultiportToReaction {
    state s:number(6);
    reaction(b.out) {=
        let sum = 0;
        for (let i = 0; i < b.out.length; i++) {
            let val = b.out[i]
            if (val !== undefined) sum += val;
        }
        console.log("Sum of received: " + sum + ".");
        if (sum != s) {
            util.requestErrorStop("ERROR: Expected " + s + ".");
        }
        s += 16;
    =}
    reaction(shutdown) {=
        if (s <= 6) {
            util.reportError("ERROR: Destination received no input!");
        }
        console.log("Success.");
    =}
    b = new Source(width = 4);
}
