// Test bank of reactors to multiport input with id parameter in the bank.
target Cpp;
reactor Source(bank_position:unsigned(0)) {
    output out:unsigned;
    
    reaction (startup) -> out {=
        out.set(bank_position);
    =}
}

reactor Sink {
    input[4] in:unsigned;
    state received:bool(false);
    
    reaction (in) {=
        for (unsigned i = 0; i < in.size(); i++) {
            received = true;
            std::cout << "Received " << *in[i].get() << '\n';
            if (*in[i].get() != i) {
                std::cerr << "Error: expected " << i << "!\n";
                exit(1);    
            }
        }
    =}
    reaction (shutdown) {=
        if (!received) {
            std::cerr << "Error: received no input!\n";
            exit(2);    
        }
    =}
}

main reactor BankToMultiport {
    source = new[4] Source();
    sink = new Sink();
    source.out -> sink.in;
}