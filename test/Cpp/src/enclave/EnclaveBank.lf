// This is a smoke test for an enclaved bank
target Cpp {
    timeout: 1 sec
}

reactor Node(
    bank_index: size_t(0),
    id: std::string({= "node" + std::to_string(bank_index) =}),
    period: time(100 msec),
    duration: time(20 msec)
) {
    logical action a: void

    reaction(startup, a) -> a {=
        reactor::log::Info() << id << " reaction executes.";
        std::this_thread::sleep_for(duration);
        reactor::log::Info() << id << " reaction done.";
        a.schedule(period);
    =} deadline(200 msec) {=
        reactor::log::Error() << id << " deadline was violated!";
        exit(1);
    =}
}

main reactor {
    slow = new Node(id = "slow", period = 500 msec, duration = 300 msec)
    @enclave
    nodes = new[4] Node()
}
