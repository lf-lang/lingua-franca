// Check multiport output to multiport input, where the latter is a hierarchical reactor. Note that
// the destination reactor has width wider than the sender, so one input is dangling.
target Cpp {
  timeout: 2 sec,
  fast: true
}

reactor Source(width: size_t = 4) {
  timer t(0, 200 msec)
  output[width] out: int
  state s: int = 0

  reaction(t) -> out {=
    for(size_t i = 0; i < 4; i++) {
      out[i].set(s++);
    }
  =}
}

reactor Destination(width: size_t = 4) {
  state s: int = 6
  input[width] in: int

  reaction(in) {=
    int sum = 0;
    for (auto i : in.present_indices_unsorted()) {
      sum += *in[i].get();
    }

    std::cout << "Sum of received: " << sum << ".\n";
    if (sum != s) {
      std::cerr << "ERROR: Expected " << s << ".\n";
      exit(1);
    }
    s += 16;
  =}

  reaction(shutdown) {=
    if (s <= 6) {
      std::cerr << "ERROR: Destination received no input!\n";
      exit(1);
    }
    std::cout << "Success.\n";
  =}
}

reactor Container(width: size_t = 4) {
  input[width] in: int
  dst = new Destination()
  in -> dst.in
}

main reactor MultiportToHierarchy(width: size_t = 4) {
  a = new Source(width=width)
  b = new Container(width=width)
  a.out -> b.in
}
