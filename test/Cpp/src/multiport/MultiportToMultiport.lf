target Cpp

reactor Source {
  output[4] out: unsigned

  reaction(startup) -> out {=
    for (unsigned i = 0; i < out.size(); i++) {
      out[i].set(i);
    }
  =}
}

reactor Sink {
  input[4] in: unsigned
  state received: bool = false

  reaction(in) {=
    for (unsigned i = 0; i < in.size(); i++) {
      std::cout << "Received " << *in[i].get() << '\n';
      received = true;
      if (*in[i].get() != i) {
        std::cerr << "Error: expected " << i << "!\n";
        exit(1);
      }
    }
  =}

  reaction(shutdown) {=
    if (!received) {
      std::cerr << "Error: No data received!\n";
      exit(2);
    }
  =}
}

main reactor MultiportToMultiport {
  source = new Source()
  sink = new Sink()
  source.out -> sink.in
}
