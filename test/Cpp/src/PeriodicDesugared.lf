target Cpp {
  fast: true,
  timeout: 5 secs
}

main reactor(offset: time = 50 msec, period: time = 500 msec) {
  logical action init(offset): void
  logical action recur(period): void
  state expected = offset

  reaction(startup) -> init {=
    std::cout << "Hello from Periodic!\n";
    init.schedule();
  =}

  reaction(init) -> recur {= recur.schedule(); =}

  reaction(init, recur) -> recur {=
    std::cout << "Periodic trigger!\n";
    auto logical_time = get_elapsed_logical_time();
    std::cout << "Elapsed logical time: " << logical_time << '\n';
    if (logical_time != expected) {
      std::cerr << "ERROR: expected " << expected << '\n';
      exit(1);
    }
    expected += period;
    recur.schedule();
  =}
}
