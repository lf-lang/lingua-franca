/**
 * This test checks that a downstream reaction is triggered only if its trigger is present.
 *
 * @author Maiko Brants TU Dresden
 *
 * Modeled after the C version of this test.
 */
target Cpp {
  timeout: 1 sec,
  fast: true
}

reactor Source {
  output[2] out: int
  state count: int = 0
  timer t(0, 200 msec)

  reaction(t) -> out {=
    if(count++ % 2 == 0) {
      out[0].set(count);
    } else {
      out[1].set(count);
    }
  =}
}

reactor Destination {
  input in: int

  reaction(in) {=
    if(!in.is_present()){
      reactor::log::Error() << "Reaction to input of triggered even though all inputs are absent!";
      exit(1);
    }
  =}
}

main reactor TriggerDownstreamOnlyIfPresent2 {
  s = new Source()
  d = new[2] Destination()
  s.out -> d.in
}
