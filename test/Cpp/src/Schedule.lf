// Example from Schedule section of the C++ Reactor Target wiki page.
target Cpp

reactor ScheduleTest {
  input x: int
  logical action a: void

  reaction(x) -> a {= a.schedule(200ms); =}

  reaction(a) {=
    auto elapsed_time = get_elapsed_logical_time();
    std::cout << "Action triggered at logical time " << elapsed_time.count()
          << " after start" << std::endl;
    if (elapsed_time != 200ms) {
      std::cerr << "Expected action time to be 200 msec. It was "
            << elapsed_time.count() << "nsec!" << std::endl;
      exit(1);
    }
  =}
}

main reactor Schedule {
  a = new ScheduleTest()
  timer t

  reaction(t) -> a.x {= a.x.set(1); =}
}
