/**
 * @author Maiko Brants TU Dresden
 *
 * Modeled after the C version of this test.
 */
target Cpp {
  timeout: 1 sec,
  fast: true
}

reactor Self {
  input x: int
  output y: int
  logical action a: int
  state expected: int = 43

  reaction(a) -> y {=
    reactor::log::Info() << "a = " << *a.get();
    y.set(*a.get()+1);
  =}

  reaction(x) -> a {=
    reactor::log::Info() << "x = " << *x.get();
    if(*x.get() != expected){
      reactor::log::Error() << "Expected " << expected;
      exit(1);
    }
    expected++;
    a.schedule(x.get(), 100ms);
  =}

  reaction(startup) -> a {= a.schedule(42, 0ns); =}

  reaction(shutdown) {=
    if(expected <= 43) {
      reactor::log::Error() << "Received no data.";
      exit(2);
    }
  =}
}

main reactor SelfLoop {
  u = new Self()
  u.y -> u.x
}
