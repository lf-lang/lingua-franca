// Test data transport across hierarchy.
target Cpp {
  ros2: true,
  ros2-dependencies: ["std_msgs"],
  timeout: 3 sec
}

public preamble {=
    #include "rclcpp/rclcpp.hpp"
    #include "std_msgs/msg/int64.hpp"

    // FIXME: forward declaration to make the node visible
    extern rclcpp::Node* lf_node;
=}

reactor Source {
  output out: std_msgs::msg::Int64
  timer t

  reaction(t) -> out {=
      RCLCPP_INFO_STREAM(lf_node->get_logger(), "source sending");
      std_msgs::msg::Int64 i;
      i.data = 1;
      out.set(i);
  =}
}

reactor Gain {
  input in: std_msgs::msg::Int64
  output out: std_msgs::msg::Int64

  reaction(in) -> out {=
    std_msgs::msg::Int64 i;
    RCLCPP_INFO_STREAM(lf_node->get_logger(), "gain gaining");
    i.data = in.get()->data*2;
    out.set(i);
  =}
}

reactor Print {
  input in: std_msgs::msg::Int64

  reaction(in) {=
    std_msgs::msg::Int64 i = *in.get();
    RCLCPP_INFO_STREAM(lf_node->get_logger(), "Received: " << i.data);
    if (i.data != 2) {
      RCLCPP_INFO_STREAM(lf_node->get_logger(), "Expected 2");
      exit(1);
    }
  =}
}



reactor GainContainer {
  input in_cont: std_msgs::msg::Int64
  output out_cont: std_msgs::msg::Int64

  @federate
  gain = new Gain()
  in_cont -> gain.in

  reaction(in_cont)  {=
      //std_msgs::msg::Int64 i;
      RCLCPP_INFO_STREAM(lf_node->get_logger(), "gain container reacting");
      //i.data = in.get()->data*2;
      // this does not work since gain instance does not exist inside of GainContainer if it is a federate
      // could be implemented with stubs or use relays (see FederateHierarchicalConnections3)
      // However, it will still not work until zero delay loop problem is solved
      //gain.in.set(i); 
  =}

  gain.out -> out_cont
}

main reactor {
  source = new Source()

  @federate
  container = new GainContainer()

  print = new Print()

  source.out -> container.in_cont
  container.out_cont -> print.in after 1s
}
