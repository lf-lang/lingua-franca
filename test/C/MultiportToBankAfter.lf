// Check multiport output to bank of recipients where the width of the bank is inferred.
target C {
    timeout: 2 sec,
    fast: true
}; 
reactor Source {
    output[3] out:int;
    reaction(startup) -> out {=
        for(int i = 0; i < out_width; i++) {
            SET(out[i], i);
        }
    =}
}
reactor Destination(instance:int(0)) {
    input in:int;
    state received:bool(false);
    reaction(in) {=
        printf("Destination %d received %d.\n", self->instance, in->value);
        if (self->instance != in->value) {
            printf("ERROR: Expected %d.\n", self->instance);
            exit(1);
        }
        if (get_elapsed_logical_time() != SEC(1)) {
            printf("ERROR: Expected to receive input after one second.\n");
            exit(2);
        }
        self->received = true;
    =}
    reaction(shutdown) {=
        if (!self->received) {
            fprintf(stderr, "ERROR: Destination %d received no input!\n", self->instance);
            exit(3);
        }
        printf("Success.\n");
    =}
}

main reactor MultiportToBank { 
    a = new Source();
    b = new[3] Destination();
    a.out -> b.in after 1 sec; // Width of the bank of delays will be inferred.
}