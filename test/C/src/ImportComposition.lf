// This tests the ability to import a reactor definition that itself imports a reactor definition.
target C

import ImportedComposition from "lib/ImportedComposition.lf"

main reactor ImportComposition {
  a = new ImportedComposition()
  state received: bool = false

  reaction(startup) -> a.x {=
    lf_set(a.x, 42);
  =}

  reaction(a.y) {=
    interval_t receive_time = lf_time_logical_elapsed();
    printf("Received %d at time " PRINTF_TIME "\n", a.y->value, receive_time);
    self->received = true;
    if (receive_time != 55000000LL) {
      fprintf(stderr, "ERROR: Received time should have been 55,000,000.\n");
      exit(1);
    }
    if (a.y->value != 42 * 2 * 2) {
      fprintf(stderr, "ERROR: Received value should have been %d.\n", 42 * 2 * 2);
      exit(2);
    }
  =}

  reaction(shutdown) {=
    if (!self->received) {
      fprintf(stderr, "ERROR: Nothing received.\n");
      exit(3);
    }
  =}
}
