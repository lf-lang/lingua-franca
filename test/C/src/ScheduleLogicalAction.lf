// This checks that a logical action is scheduled the specified logical time after the current
// logical time.
target C {
  fast: true,
  timeout: 3 sec
}

reactor foo {
  input x: int
  output y: int
  logical action a: int*

  reaction(x) -> y, a {=
    lf_set(y, 2*x->value);
    // The following uses physical time, incorrectly.
    lf_schedule(a, MSEC(500));
  =}

  reaction(a) -> y {=
    lf_set(y, -42);
  =}
}

reactor print {
  state expected_time: time = 0
  input x: int

  reaction(x) {=
    interval_t elapsed_time = lf_time_logical_elapsed();
    printf("Result is %d\n", x->value);
    printf("Current logical time is: " PRINTF_TIME "\n", elapsed_time);
    printf("Current physical time is: " PRINTF_TIME "\n", lf_time_physical_elapsed());
    if (elapsed_time != self->expected_time) {
      printf("ERROR: Expected logical time to be " PRINTF_TIME ".\n", self->expected_time);
      exit(1);
    }
    self->expected_time += MSEC(500);
  =}
}

main reactor {
  f = new foo()
  p = new print()
  timer t(0, 1 sec)
  f.y -> p.x

  reaction(t) -> f.x {=
    lf_set(f.x, 42);
  =}
}
