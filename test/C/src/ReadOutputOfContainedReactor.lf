// Test reacting to and reading outputs from a contained reactor in various permutations.
target C

reactor Contained {
  output out: int

  reaction(startup) -> out {=
    lf_set(out, 42);
  =}
}

main reactor ReadOutputOfContainedReactor {
  c = new Contained()
  state count: int = 0

  reaction(startup) c.out {=
    printf("Startup reaction reading output of contained reactor: %d.\n", c.out->value);
    if (c.out->value != 42) {
      fprintf(stderr, "Expected 42!\n");
      exit(2);
    }
    self->count++;
  =}

  reaction(c.out) {=
    printf("Reading output of contained reactor: %d.\n", c.out->value);
    if (c.out->value != 42) {
      fprintf(stderr, "Expected 42!\n");
      exit(3);
    }
    self->count++;
  =}

  reaction(startup, c.out) {=
    printf("Alternate triggering reading output of contained reactor: %d.\n", c.out->value);
    if (c.out->value != 42) {
      fprintf(stderr, "Expected 42!\n");
      exit(4);
    }
    self->count++;
  =}

  reaction(shutdown) {=
    if (self->count != 3) {
      printf("FAILURE: One of the reactions failed to trigger.\n");
      exit(1);
    } else {
      printf("Test passes.\n");
    }
  =}
}
