// This checks that the after keyword adjusts logical time, not using physical time.
target C {
  fast: false,
  timeout: 3 sec
}

reactor foo {
  input x: int
  output y: int

  reaction(x) -> y {=
    lf_set(y, 2*x->value);
  =}
}

reactor print {
  state expected_time: time = 0
  state received: int = 0
  input x: int

  reaction(x) {=
    self->received++;
    interval_t elapsed_time = lf_time_logical_elapsed();
    printf("Result is %d\n", x->value);
    if (x->value != 84) {
      printf("ERROR: Expected result to be 84.\n");
      exit(1);
    }
    printf("Current logical time is: " PRINTF_TIME "\n", elapsed_time);
    printf("Current microstep is: " PRINTF_TIME "\n", lf_tag().microstep);
    printf("Current physical time is: " PRINTF_TIME "\n", lf_time_physical_elapsed());
    if (elapsed_time != self->expected_time) {
      printf("ERROR: Expected logical time to be " PRINTF_TIME ".\n", self->expected_time);
      exit(2);
    }
    if (lf_tag().microstep != 1) {
      printf("ERROR: Expected microstep to be 1\n");
      exit(3);
    }
    self->expected_time += SEC(1);
  =}

  reaction(shutdown) {=
    if (self->received == 0) {
      printf("ERROR: Final reactor received no data.\n");
      exit(3);
    }
  =}
}

main reactor {
  f = new foo()
  p = new print()
  timer t(0, 1 sec)
  f.y -> p.x after 0

  reaction(t) -> f.x {=
    lf_set(f.x, 42);
  =}
}
