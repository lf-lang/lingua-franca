// This tests the mechanism for testing whether a previous reaction has produced a given output. The
// output should always be 42.
target C

preamble {=
  #ifdef __cplusplus
  extern "C" {
  #endif
  #include <time.h>
  #ifdef __cplusplus
  }
  #endif
=}

reactor Source {
  output out: int

  reaction(startup) -> out {=
    // Set a seed for random number generation based on the current time.
    srand(time(0));
    // Randomly produce an output or not.
    if (rand() % 2) {
      lf_set(out, 21);
    }
  =}

  reaction(startup) -> out {=
    if (out->is_present) {
      lf_set(out, 2 * out->value);
    } else {
      lf_set(out, 42);
    }
  =}
}

reactor Sink {
  input in: int

  reaction(in) {=
    printf("Received %d.\n", in->value);
    if (in->value != 42) {
      fprintf(stderr, "FAILED: Expected 42.\n");
      exit(1);
    }
  =}
}

main reactor TestForPreviousOutput {
  s = new Source()
  d = new Sink()
  s.out -> d.in
}
