target C {
  scheduler: STATIC
}

preamble {=
  #include "include/core/platform.h"
=}

main reactor {
  preamble {=
    // Thread to read input characters until an EOF is received.
    // Each time a newline is received, schedule a user_response action.
    void* read_input(void* physical_action) {
        int c;
        while(1) {
            while((c = getchar()) != '\n') {
            if (c == EOF) break;
            }
            lf_schedule_copy(physical_action, 0, &c, 1);
            if (c == EOF) break;
        }
        return NULL;
    }
  =}

  timer t(1 sec, 1 sec)
  physical action a(0, 500 msec): char

  reaction(startup) -> a {=
    // Start the thread that listens for Enter or Return.
    lf_thread_t thread_id;
    lf_thread_create(&thread_id, &read_input, a);
  =}

  reaction(t) {=
    lf_print("Hello @ %lld", lf_time_logical_elapsed());
  =}

  reaction(a) {=
    lf_print("Surprise @ %lld", lf_time_logical_elapsed());
  =}
}
