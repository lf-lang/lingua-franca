/** This tests an output that is broadcast back to a multiport input of a bank. */
target C {
  timeout: 1 sec,
  build-type: RelWithDebInfo
}

reactor SenderAndReceiver {
  output out: int
  input[2] in: int
  state received: bool = false

  reaction(startup) -> out {=
    lf_set(out, 42);
  =}

  reaction(in) {=
    if (in[0]->is_present && in[1]->is_present && in[0]->value == 42 && in[1]->value == 42) {
      lf_print("SUCCESS");
      self->received = true;
    }
  =}

  reaction(shutdown) {=
    if (!self->received == true) {
      lf_print_error_and_exit("Failed to receive broadcast");
    }
  =}
}

federated reactor {
  s = new[2] SenderAndReceiver()
  (s.out)+ -> s.in
}
