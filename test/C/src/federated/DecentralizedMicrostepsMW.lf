/**
 * @brief A federated system with a decentralized coordinator that has large `maxwait` and
 * `absent_after` offsets.
 *
 * This test verifies that a large `maxwait` and `absent_after` offset can be used when the data
 * flow is predictable even if the the program is lagging behind physical time. This version uses
 * microsteps and never advances past the start time. It also uses lf_request_stop() to stop the
 * execution.
 *
 * @author Edward A. Lee
 */
target C {
  coordination: decentralized
}

reactor Pi {
  input trigger: bool
  output out: int

  reaction(trigger) -> out {=
    tag_t now = lf_tag();
    lf_print("***** at tag " PRINTF_TAG, now.time - lf_time_start(), now.microstep);
    lf_set(out, 42);
  =} tardy {=
    tag_t now = lf_tag();
    lf_print_error_and_exit("STP violation at Pi at tag " PRINTF_TAG, now.time - lf_time_start(), now.microstep);
  =}
}

reactor Gather {
  input[4] in: int
  output next: bool
  logical action a
  state count: int = 0

  reaction(startup, a) -> next {=
    lf_set(next, true);
  =}

  reaction(in) -> a {=
    tag_t now = lf_tag();
    for (int i = 0; i < 4; i++) {
      if (!in[i]->is_present) {
        lf_print_error_and_exit("Missing input %d in Gather at tag " PRINTF_TAG,
            i, now.time - lf_time_start(), now.microstep);
      }
    }
    lf_print("%d: at tag " PRINTF_TAG, self->count, now.time - lf_time_start(), now.microstep);
    self->count++;
    if (self->count >= 5) {
      lf_request_stop(); // `absent_after` is not applied at the stop tag, so we can't count on communication completing.
    }
    lf_schedule(a, 0);
  =} tardy {=
    tag_t now = lf_tag();
    lf_print_error_and_exit("STP violation at Gather at tag " PRINTF_TAG, now.time - lf_time_start(), now.microstep);
  =}

  reaction(shutdown) {=
    if (self->count < 5) {
      lf_print_error_and_exit("Gather received only %d inputs. Expected at least 5.", self->count);
    }
  =}
}

federated reactor {
  // Default maxwait of zero works because Pi's event queue only becomes non-empty when it receives an input.
  pi = new[4] Pi()
  // Default maxwait of zero allows Gather to produce an output.
  g = new Gather()
  // An absent_after of forever prevents this federate from continuing until it gets all four inputs.
  @absent_after(forever)
  pi.out -> g.in
  (g.next)+ -> pi.trigger
}
