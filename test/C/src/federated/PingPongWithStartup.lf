// The problem is that start signal arrives at Ping after it has completed (0,0),
// in which it has events because of the startup functions in the network receiver
// reactors. The event gets put on the event queue with tag (0,1), but then Ping
// stays blocked on the receive input which has STAA of forever.
target C {
  coordination: decentralized  // logging: log
}

reactor Ping(count: size_t = 10, STA: time = 0) {
  input receive: size_t
  input start: bool
  output send: size_t
  output finished: bool
  state pingsLeft: size_t = count
  logical action serve

  reaction(start, serve) -> send {=
    lf_set(send, self->pingsLeft--);
    lf_print("**************************** Ping sent");
  =}

  reaction(receive) -> serve, finished {=
    lf_print("**************************** Ping received");
    if (self->pingsLeft > 0) {
        lf_schedule(serve, MSEC(10));
    } else {
        self->pingsLeft = self->count;
        lf_set(finished, true);
    }
  =} STAA(forever) {=  =}
}

reactor Pong(expected: size_t = 10) {
  input receive: size_t
  output send: size_t
  state count: size_t = 0

  reaction(receive) -> send {=
    lf_print("**************************** Pong received and sent");
    self->count++;
    lf_set(send, receive->value);
  =} STAA(forever) {=  =}

  reaction(shutdown) {=
    if (self->count != self->expected) {
        lf_print_error_and_exit("Pong expected to receive %zu inputs, but it received %zu.\n",
            self->expected, self->count
        );
        exit(1);
    }
    lf_print("Success.\n");
  =}
}

reactor Startup(STA: time = 0) {
  input finish: bool
  output start: bool

  reaction(startup) -> start {=
    lf_print("Starting benchmark.\n");
    lf_set(start, true);
  =}

  reaction(finish) {=
    lf_print("Benchmark finished.\n");
    lf_request_stop();
  =}
}

federated reactor(numIterations: size_t = 12, count: size_t = 10) {
  runner = new Startup()
  ping = new Ping(count=count)
  pong = new Pong(expected=count)

  runner.start -> ping.start
  ping.finished -> runner.finish
  ping.send -> pong.receive
  pong.send -> ping.receive
}
