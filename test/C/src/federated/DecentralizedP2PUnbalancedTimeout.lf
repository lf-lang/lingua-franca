/**
 * Test a source-destination scenario where the source falls behind real-time, and reaches the
 * timeout much later than the destination. In this test, the destination closes the connection
 * early, causing the transmission to fail. Warnings will be printed about lost messages.
 *
 * The test fails if the federation does not exit.
 */
target C {
  timeout: 1 msec,
  coordination: decentralized
}

reactor Clock(offset: time = 0, period: time = 1 sec) {
  output y: int
  timer t(offset, period)
  state count: int = 0

  reaction(t) -> y {=
    (self->count)++;
    lf_print("Sending %d.", self->count);
    lf_set(y, self->count);
  =}

  reaction(shutdown) {=
    lf_print("SUCCESS: the source exited successfully.");
  =}
}

reactor Destination {
  input x: int
  state s: int = 1

  reaction(x) {=
    lf_print("Received %d", x->value);
    tag_t current_tag = lf_tag();
    if (x->value != self->s) {
      lf_print_error_and_exit("At tag " PRINTF_TAG " expected %d and got %d.",
        current_tag.time - lf_time_start(), current_tag.microstep, self->s, x->value
      );
    }
    self->s++;
  =}

  reaction(shutdown) {=
    lf_print("**** shutdown reaction invoked.");
    lf_print("Approx. time per reaction: " PRINTF_TIME " ns", lf_time_physical_elapsed()/(self->s+1));
  =}
}

federated reactor(period: time = 10 usec) {
  c = new Clock(period=period)
  d = new Destination()
  c.y -> d.x
}
