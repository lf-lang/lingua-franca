/** 
 * Test a form of a distributed deterministic system
 * where a federate that receives timestamped messages has a timer in addition to the messages
 * as triggers. Therefore, careful coordination of the advancement of time using Ptides is needed.
 * In addition, this test shows that the tardiness is passed down the hierarchy until it is handled.
 * @author Edward A. Lee
 * @author Soroush Bateni
 */
target C {
    timeout: 4900 msec,
    coordination: decentralized
};

import Count from "../lib/Count.lf";

reactor ImportantActuator {
    input in:int;
    state success:int(0);
    state success_tardy:int(0);
    timer t(0, 10 usec); // Force a timer to be invoke periodically
                         // to ensure logical time will advance in the
                         // absence of incoming messages.
    state c:int(0);
    reaction(in) {=
        tag_t current_tag = get_current_tag();
        printf("At tag (%lld, %u) received %d. Intended tag is (%lld, %u).\n",
            get_elapsed_logical_time(),
            get_microstep(),
            in->value,
            in->intended_tag.time - get_start_time(),
            in->intended_tag.microstep);
        if (compare_tags((tag_t){.time=current_tag.time - get_start_time(), .microstep=current_tag.microstep}, 
                         (tag_t){.time=SEC(1) *  self->c, .microstep=0}) == 0) {
            self->success++; // Message was on-time
        }
        self->c++;
    =} tardy {=
        tag_t current_tag = get_current_tag();
        printf("At tag (%lld, %u), message is tardy by (%lld, %u).\n",
                current_tag.time - start_time, current_tag.microstep,
                current_tag.time - in->intended_tag.time,
                current_tag.microstep - in->intended_tag.microstep);
        self->success_tardy++;
        self->c++;
    =}
    reaction(t) {=
        // Do nothing.
    =}
    
    reaction(shutdown) {=
        if ((self->success +  self->success_tardy) != 5) {
            fprintf(stderr, "Failed to detect tardiness of messages.\n");
            exit(1);
        } else {
            printf("Successfully detected tardiness (%d tardy, %d on-time).\n", self->success_tardy, self->success);
        }
    =}
}

reactor Print {
    input in:int;
    reaction(in) {=
        tag_t current_tag = get_current_tag();  
        printf("At tag (%lld, %u) received %d. Intended tag is (%lld, %u).\n",
               current_tag.time - get_start_time(),
               current_tag.microstep,
               in->value,
               in->intended_tag.time - get_start_time(),
               in->intended_tag.microstep);
    =}

}

reactor Receiver {
    input in:int;
    timer t(0, 10 msec); // Force a timer to be invoke periodically
                         // to ensure logical time will advance in the
                         // absence of incoming messages.
    state c:int(0);
    p = new Print();
    a = new ImportantActuator();
    reaction(in) -> p.in, a.in {=
        SET(p.in, in->value + 1);
        SET(a.in, in->value + 1);
    =}
    
    reaction(t) {=
        // Do nothing.
    =}
}

federated reactor {
    c = new Count();
    r = new Receiver();
    c.out -> r.in;    // Indicating a 'logical' connection.
}
