/**
 * This tests a feedback loop with physical actions and centralized coordination.
 *
 * @author Edward A. Lee
 */
target C {
  coordination: centralized,
  coordination-options: {
    advance-message-interval: 100 msec
  },
  timeout: 5 sec,
  logging: warn
}

preamble {=
  #include <unistd.h> // Defines sleep()
  extern bool stop;
  void* ping(void* actionref);
=}

reactor Looper(incr: int = 1, delay: time = 0 msec) {
  preamble {=
    bool stop = false;
    // Thread to trigger an action once every second.
    void* ping(void* actionref) {
      while(!stop) {
        lf_print("Scheduling action.");
        lf_schedule(actionref, 0);
        sleep(1);
      }
      return NULL;
    }
  =}
  input in: int
  input in2: int
  output out: int
  output out2: int
  physical action a(delay)
  state count: int = 0
  timer t(0, 1 sec)

  reaction(startup) -> a {=
    // Start the thread that listens for Enter or Return.
    lf_thread_t thread_id;
    lf_print("Starting thread.");
    lf_thread_create(&thread_id, &ping, a);
  =}

  reaction(a) -> out, out2 {=
    if (self->count%2 == 0) {
      lf_set(out, self->count);
    } else {
      lf_set(out2, self->count);
    }
    self->count += self->incr;
  =}

  reaction(in) {=
    tag_t current_tag = lf_tag();
    lf_print("Received %d at logical time " PRINTF_TAG ".",
      in->value,
      current_tag.time - lf_time_start(), current_tag.microstep
    );
  =}

  reaction(in2) {=
    tag_t current_tag = lf_tag();
    lf_print("Received %d on in2 at logical time " PRINTF_TAG ".",
      in2->value,
      current_tag.time - lf_time_start(), current_tag.microstep
    );
  =}

  reaction(t) {=
    tag_t current_tag = lf_tag();
    lf_print("Timer triggered at logical time " PRINTF_TAG ".",
      current_tag.time - lf_time_start(), current_tag.microstep
    );
  =}

  reaction(shutdown) {=
    lf_print("******* Shutdown invoked.");
    // Stop the thread that is scheduling actions.
    stop = true;
    if (self->count != 5 * self->incr) {
      lf_print_error_and_exit("Failed to receive all five expected inputs.");
    }
  =}
}

federated reactor(delay: time = 0) {
  left = new Looper()
  right = new Looper(incr=-1)
  left.out -> right.in
  right.out -> left.in
  right.out2 -> left.in2
  left.out2 -> right.in2
}
