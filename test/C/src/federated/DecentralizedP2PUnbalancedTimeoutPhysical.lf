/**
 * Test a source-destination scenario where the source falls behind real-time, and reaches the
 * timeout much later than the destination. In this test, the destination closes the connection
 * early, causing the transmission to fail. Warnings will be printed.
 *
 * The test fails if the federation does not exit normally. This variant has a physical connection
 * between source and destination.
 */
target C {
  timeout: 1 msec,
  coordination: decentralized
}

reactor Clock(offset: time = 0, period: time = 1 sec) {
  output y: int
  timer t(offset, period)
  state count: int = 0

  reaction(t) -> y {=
    (self->count)++;
    //printf("Reacting at time " PRINTF_TIME ".\n", lf_time_logical_elapsed());
    lf_set(y, self->count);
  =}

  reaction(shutdown) {=
    lf_print("SUCCESS: the source exited successfully.");
  =}
}

reactor Destination {
  input x: int
  state s: int = 1

  reaction(x) {=
    // printf("%d\n", x->value);
    if (x->value != self->s) {
      lf_print_error_and_exit("Expected %d and got %d.", self->s, x->value);
    }
    self->s++;
  =}

  reaction(shutdown) {=
    lf_print("**** shutdown reaction invoked.");
    lf_print("Approx. time per reaction: " PRINTF_TIME " ns", lf_time_physical_elapsed()/(self->s+1));
  =}
}

federated reactor(period: time = 10 usec) {
  c = new Clock(period=period)
  // This needs a `maxwait` because otherwise it may advance to the stop time while the
  // physical connection is in the process of assigning a timestamp.
  @maxwait(1 ms)
  d = new Destination()
  c.y ~> d.x
}
