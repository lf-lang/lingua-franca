target C

reactor Destination {
  input x: int
  input y: int

  reaction(x, y) {=
    interval_t elapsed = lf_time_logical_elapsed();
    printf("Time since start: " PRINTF_TIME ".\n", elapsed);
    if (elapsed != 0LL) {
      printf("Expected elapsed time to be 0, but it was " PRINTF_TIME ".\n", elapsed);
      exit(1);
    }
    int count = 0;
    if (x->is_present) {
      printf("  x is present.\n");
      count++;
    }
    if (y->is_present) {
      printf("  y is present.\n");
      count++;
    }
    if (count != 1) {
      printf("Expected exactly one input to be present but got %d.\n", count);
      exit(1);
    }
  =}
}

main reactor Microsteps {
  timer start
  logical action repeat
  d = new Destination()

  reaction(start) -> d.x, repeat {=
    lf_set(d.x, 1);
    lf_schedule(repeat, 0);
  =}

  reaction(repeat) -> d.y {=
    lf_set(d.y, 1);
  =}
}
