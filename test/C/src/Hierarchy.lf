// Test data transport across hierarchy.
target C

reactor Source {
  output out: int
  timer t

  reaction(t) -> out {=
    lf_set(out, 1);
  =}
}

reactor Gain {
  input in: int
  output out: int

  reaction(in) -> out {=
    printf("Gain received %d.\n", in->value);
    lf_set(out, in->value * 2);
  =}
}

reactor Print {
  input in: int

  reaction(in) {=
    printf("Received: %d.\n", in->value);
    if (in->value != 2) {
      printf("Expected 2.\n");
      exit(1);
    }
  =}
}

reactor GainContainer {
  input in: int
  output out: int
  output out2: int
  gain = new Gain()
  in -> gain.in
  gain.out -> out
  gain.out -> out2
}

main reactor Hierarchy {
  source = new Source()
  container = new GainContainer()
  print = new Print()
  print2 = new Print()
  source.out -> container.in
  container.out -> print.in
  container.out2 -> print2.in
}
