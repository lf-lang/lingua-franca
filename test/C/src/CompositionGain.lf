// This tests send data through a contained reactor.
target C

reactor Gain {
  input gainin: int
  output y: int

  reaction(gainin) -> y {=
    printf("Gain received %d\n", gainin->value);
    lf_set(y, gainin->value * 2);
  =}
}

reactor Wrapper {
  input x: int
  output y: int
  gain = new Gain()
  x -> gain.gainin
  gain.y -> y
}

main reactor CompositionGain {
  wrapper = new Wrapper()

  reaction(startup) -> wrapper.x {=
    lf_set(wrapper.x, 42);
  =}

  reaction(wrapper.y) {=
    printf("Received %d\n", wrapper.y->value);
    if (wrapper.y->value != 42 * 2) {
      fprintf(stderr, "ERROR: Received value should have been %d.\n", 42 * 2);
      exit(2);
    }
  =}
}
