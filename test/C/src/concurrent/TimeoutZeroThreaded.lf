/*
 * A test for the timeout functionality in Lingua Franca.
 * This variant tests timeout at (0,0) and uses the threaded
 * runtime.
 * 
 * @author Soroush Bateni
 */
 target C {
    timeout: 0 sec,
};

import Sender from "../lib/LoopedActionSender.lf"

reactor Consumer {
    input in:int;
    state success:bool(false);
    reaction(in) {=
        tag_t current_tag = get_current_tag();
        if (compare_tags(current_tag,
                         (tag_t) { .time = MSEC(0) + get_start_time(), .microstep = 0}) > 0) {
        	fprintf(stderr,"ERROR: Tag (%lld, %d) received. Failed to enforce timeout.\n",
                            current_tag.time - start_time, current_tag.microstep);
        	exit(1);
        } else if (compare_tags(current_tag,
                         (tag_t) { .time = MSEC(0) + get_start_time(), .microstep = 0}) == 0) {
            self->success = true; // Successfully invoked the reaction at (timeout, 0)
        }
    =}
    
    reaction(shutdown) {=
        printf("Shutdown invoked at tag (%lld, %u).\n", current_tag.time - get_start_time(), current_tag.microstep);
        if (compare_tags(current_tag,
            (tag_t) { .time = MSEC(0) + get_start_time(), .microstep = 0}) == 0 &&
            self->success == true) {
            printf("SUCCESS: successfully enforced timeout.\n");
        } else {
        	fprintf(stderr,"ERROR: Shutdown invoked at tag (%llu, %d). Failed to enforce timeout.\n",
                            current_tag.time - start_time, current_tag.microstep);
        	exit(1);
        }
    =}
} 

main reactor {
   consumer = new[4] Consumer();
   producer = new[4] Sender(break_interval = 1 msec); 
   
   producer.out -> consumer.in;
}
