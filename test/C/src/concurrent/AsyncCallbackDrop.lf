// Test asynchronous callbacks that trigger a physical action with a DROP policy. This test case
// assumes that the target is multithreaded. This test will not work with the unthreaded C target
// because that target does not implement any mutex protecting the event queue.
target C {
  keepalive: true,  // To silence warning.
  timeout: 2 sec
}

preamble {=
  #ifdef __cplusplus
  extern "C" {
  #endif
  #include "platform.h"
  #ifdef __cplusplus
  }
  #endif
=}

main reactor {
  preamble {=
    void callback(void* a) {
      // Schedule twice in rapid succession.
      // The second value should be dropped because the
      // timestamps will not be sufficiently separated.
      // The minimum time between these is determined
      // by the argument in the physical action definition.
      lf_schedule_int(a, 0, 0);
      lf_schedule_int(a, 0, 1);
    }
    // Simulate time passing before a callback occurs.
    void* take_time(void* a) {
      instant_t sleep_time = 100000000;
      lf_sleep(sleep_time);
      callback(a);
      return NULL;
    }
    lf_thread_t threadId;
  =}
  timer t(0, 200 msec)
  state thread_id: lf_thread_t = 0
  state expected_time: time = 100 msec
  state toggle: bool = false

  physical action a(100 msec, 100 msec, "drop"): int
  state i: int = 0

  reaction(t) -> a {=
    // start new thread, provide callback
    lf_thread_create(&self->thread_id, &take_time, a);
  =}

  reaction(a) {=
    instant_t elapsed_time = lf_time_logical_elapsed();
    printf("Asynchronous callback %d: Assigned logical time greater than start time by %lld nsec.\n", self->i++, elapsed_time);
    if (elapsed_time <= self->expected_time) {
      printf("ERROR: Expected logical time to be larger than %lld.\n", self->expected_time);
      exit(1);
    }
    if (a->value != 0) {
      printf("ERROR: Received: %d, expected 0 because the second event should have been dropped.\n", a->value);
      exit(2);
    }
    if (self->toggle) {
      self->toggle = false;
      self->expected_time += 200000000LL;
    } else {
      self->toggle = true;
    }
  =}
}
