// Example in the Wiki.
target C

reactor Scale(scale: int = 2) {
  input x: int
  output y: int

  reaction(x) -> y {=
    lf_set(y, x->value * self->scale);
  =}
}

reactor Test {
  input x: int
  state received_value: bool = false

  reaction(x) {=
    printf("Received %d.\n", x->value);
    self->received_value = true;
    if (x->value != 2) {
      printf("ERROR: Expected 2!\n");
      exit(1);
    }
  =}

  reaction(shutdown) {=
    if (!self->received_value) {
      printf("ERROR: No value received by Test reactor!\n");
    } else {
      printf("Test passes.\n");
    }
  =}
}

main reactor {
  g = new Scale()
  d = new Test()
  g.y -> d.x

  reaction(startup) -> g.x {=
    lf_set(g.x, 1);
  =}
}
