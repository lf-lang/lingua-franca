/*
 * Modal Reactor Test.
 * 
 * Tests if connections in the same reactor that have the same destination work if they are located in separate modes.
 */
target C {
    fast: false,
    timeout: 1 sec
};

import TraceTesting from "util/TraceTesting.lf"

reactor Modal {
    input in1:int;
    input in2:int;
    output out:int;
    
    mode Two { // Defining reaction to in2 before in1 would cause cycle if no mode were present
        timer wait(150msec, 1sec)
              
        reaction(in2) {=
            // SET(out, in2->value);
        =}
        
        reaction(wait) -> One {=
            SET_MODE(One);
            printf("Switching to mode One\n");
        =}
    }
    initial mode One {
        reaction(in1) -> out {=
            SET(out, in1->value);
        =}

        reaction(in1) -> Two {=
            if (in1->value % 5 == 4) {
                SET_MODE(Two);
                printf("Switching to mode Two\n");
            }
        =}
    }
}

reactor Counter(period:time(1sec)) {
    output value:int
    
    timer t(0, period)
    state curval:int(0)
    
    reaction(t) -> value {=
        SET(value, self->curval++);
    =}
}

main reactor {
    counter = new Counter(period=100msec)    
    modal = new Modal()
    test = new TraceTesting(
        events_size = 1,
        trace_size = 27, 
        trace = (
            0,1,0,
            100000000,1,1,
            100000000,1,2,
            100000000,1,3,
            100000000,1,4,
            200000000,1,6,
            100000000,1,7,
            100000000,1,8,
            100000000,1,9
        ), training = false)

    
    counter.value -> modal.in1
    modal.out -> modal.in2
    
    modal.out
    -> test.events

    // Print
    reaction(modal.out) {=
        printf("%d\n", modal.out->value);
    =}
}