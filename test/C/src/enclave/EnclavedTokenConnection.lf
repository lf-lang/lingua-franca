// This tests that the enclave connections also can carry tokens and dynamically
// allocated memory
target C {
  timeout: 1 sec
}

reactor Src {
  output out: int*
  timer t(0, 100 msec)

  reaction(t) -> out {=
    int * x = (int *) malloc(sizeof(int));
    *x = 42;
    lf_set(out, x);
  =}
}

reactor Double {
  input in: int*
  output out: int*

  state check: bool = false

  reaction(in) -> out {=
    int * x = (int *) malloc(sizeof(int));
    *x = (*in->value)*2;
    lf_set(out, x);
    self->check = true;
  =}

  reaction(shutdown) {=
    LF_ASSERT(self->check, "Double reactor was not invoked");
  =}
}

reactor Dest {
  input in: int*

  state check: bool = false

  reaction(in) {=
    LF_ASSERT(*in->value == 84, "Received wrong value");
    self->check = true;
  =}

  reaction(shutdown) {=
    LF_ASSERT(self->check, "Dest reactor was not invoked");
  =}
}

main reactor {
  @enclave
  src = new Src()

  @enclave
  dbl = new Double()

  @enclave
  dest = new Dest()

  src.out -> dbl.in after 50 msec
  dbl.out -> dest.in after 50 msec
}
