// Check multiport output to multiport input, where the latter is a hierarchical reactor. Note that
// the destination reactor has width wider than the sender, so one input is dangling.
target C {
  timeout: 2 sec,
  fast: true
}

reactor Source(width: int = 2) {
  timer t(0, 200 msec)
  output[width] out: int
  state s: int = 0

  reaction(t) -> out {=
    for(int i = 0; i < 4; i++) {
      lf_set(out[i], self->s++);
    }
  =}
}

reactor Destination(width: int = 4) {
  state s: int = 6
  input[width] in: int

  reaction(in) {=
    int sum = 0;
    for (int i = 0; i < in_width; i++) {
      if (in[i]->is_present) sum += in[i]->value;
    }
    printf("Sum of received: %d.\n", sum);
    if (sum != self->s) {
      printf("ERROR: Expected %d.\n", self->s);
      exit(1);
    }
    self->s += 16;
  =}

  reaction(shutdown) {=
    if (self->s <= 6) {
      fprintf(stderr, "ERROR: Destination received no input!\n");
      exit(1);
    }
    printf("Success.\n");
  =}
}

reactor Container(width: int = 4) {
  input[width] in: int
  dst = new Destination()
  in -> dst.in
}

main reactor MultiportToHierarchy(width: int = 4) {
  a = new Source(width=width)
  b = new Container(width=width)
  a.out -> b.in
}
