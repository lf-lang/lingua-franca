target C {
  timeout: 2 sec,
  fast: true
}

reactor Source {
  output out: int

  reaction(startup) -> out {=
    lf_set(out, 42);
  =}
}

reactor Destination(bank_index: int = 0) {
  input in: int
  state received: bool = false

  reaction(in) {=
    printf("Destination %d received %d.\n", self->bank_index, in->value);
    if (in->value != 42) {
      printf("ERROR: Expected 42.\n");
      exit(1);
    }
    if (lf_time_logical_elapsed() != SEC(1)) {
      printf("ERROR: Expected to receive input after one second.\n");
      exit(2);
    }
    self->received = true;
  =}

  reaction(shutdown) {=
    if (!self->received) {
      fprintf(stderr, "ERROR: Destination %d received no input!\n", self->bank_index);
      exit(3);
    }
    printf("Success.\n");
  =}
}

main reactor {
  a = new Source()
  b = new[4] Destination()
  (a.out)+ -> b.in after 1 sec
}
