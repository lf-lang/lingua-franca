// Test multiport to multiport connections. See also MultiportToMultiport.
target C

reactor Source(width: int = 2) {
  output[width] out: int

  reaction(startup) -> out {=
    for (int i = 0; i < out_width; i++) {
      lf_set(out[i], i);
    }
  =}
}

reactor Destination(width: int = 2) {
  input[width] in: int

  reaction(in) {=
    for (int i = 0; i < in_width; i++) {
      if (in[i]->is_present) {
        printf("Received on channel %d: %d\n", i, in[i]->value);
        // NOTE: For testing purposes, this assumes the specific
        // widths instantiated below.
        if (in[i]->value != i % 3) {
          fprintf(stderr, "ERROR: expected %d!\n", i % 3);
          exit(1);
        }
      }
    }
  =}
}

main reactor MultiportToMultiport2(width1: int = 3, width2: int = 2, width3: int = 5) {
  a1 = new Source(width=width1)
  a2 = new Source(width=width2)
  b = new Destination(width=width3)
  a1.out, a2.out -> b.in
}
