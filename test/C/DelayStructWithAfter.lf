// This tests delaying a struct using after.
target C {files: include/hello.h};

preamble {=
	#include "hello.h"
=}

reactor DSWASource {
    output out:hello_t*;
    reaction(startup) -> out {=
        // Dynamically allocate an output struct.
        SET_NEW(out);
        
        // Above allocates a struct, which then must be populated.
        out->value->name = "Earth";
        out->value->value = 42;
    =}
}
// expected parameter is for testing.
reactor DSWAPrint(expected:int(42)) {
    input in:hello_t*;
    reaction(in) {=
        printf("Received: name = %s, value = %d\n", in->value->name, in->value->value);
        if (in->value->value != self->expected) {
            printf("ERROR: Expected value to be %d.\n", self->expected);
            exit(1);
        }
    =}
}
main reactor DelayStruct {
    s = new DSWASource();
    p = new DSWAPrint();
    s.out -> p.in after 100 msec;
}
