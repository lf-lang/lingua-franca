// This example illustrates local deadline handling.
// Even numbers are sent by the Source immediately, whereas odd numbers
// are sent after a big enough delay to violate the deadline.
target C {
    timeout: 2 sec,
    threads: 1
};

reactor Source(period:time(1 sec)) {
    output y:int;
    timer t(0, period);
    state count:int(0);
    reaction(t) -> y {=
        if (2 * (self->count / 2) != self->count) {
            // The count variable is odd.
            // Take time to cause a deadline violation.
            // Do not use nanosleep() here because if this is threaded,
            // it gets awakened right away.
            interval_t sleep_time = MSEC(200);
            instant_t start_time = get_physical_time();
            while (get_physical_time() < start_time + sleep_time) {};
        }
        printf("Source sends: %d.\n", self->count);
        set(y, self->count);
        (self->count)++;
    =}
}

reactor Destination(timeout:time(1 sec)) {
    input x:int;
    state count:int(0);
    reaction(x) {=
        printf("Destination receives: %d\n", x->value);
        if (2 * (self->count / 2) != self->count) {
            // The count variable is odd, so the deadline should have been violated.
            printf("ERROR: Failed to detect deadline.\n");
            exit(1);
        }
        (self->count)++;
    =} deadline(timeout) {=
        printf("Destination deadline handler receives: %d\n", x->value);
        if (2 * (self->count / 2) == self->count) {
            // The count variable is even, so the deadline should not have been violated.
            printf("ERROR: Deadline miss handler invoked without deadline violation.\n");
            exit(2);
        }
        (self->count)++;
    =}
}
main reactor Deadline {
    s = new Source();
    d = new Destination(timeout = 100 msec);
    s.y -> d.x;
}