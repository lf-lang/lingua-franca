// Tests a port connection between (input of self -> input of child)
target Rust;

reactor Source {
    output out: i32;
    reaction(startup) -> out {=
      ctx.set(out, 76600);
      println!("out := 76600")
    =}
}
reactor TestCase {
    input inp: i32;
    output out: i32;
    inp -> out;
}
reactor Sink {
    input inp: i32;
    state done: bool(false);
    reaction(inp) {=
        assert_eq!(ctx.get(inp), Some(76600));
        println!("Success");
        self.done = true;
    =}

    reaction(shutdown) {=
        assert!(self.done, "reaction was not executed")
    =}
}

main reactor {
    source = new Source();
    middle = new TestCase();
    sink = new Sink();

    source.out -> middle.inp;
    middle.out -> sink.inp;
}
