// Tests that a port value is cleaned up after a tag
target Rust

reactor Source {
  output out: u32

  reaction(startup) -> out {= ctx.set(out, 150); =}
}

reactor Sink {
  timer t2(15 msec)

  input in: u32
  state reaction_num: u32 = 0
  state done: bool = false

  reaction(in, t2) {=
    if self.reaction_num == 0 {
      assert!(matches!(ctx.get(r#in), Some(150)));
    } else {
      assert_eq!(self.reaction_num, 1);
      assert!(ctx.get(r#in).is_none(), "value should have been cleaned up");
      self.done = true;
    }
    self.reaction_num += 1;
  =}

  reaction(shutdown) {=
    assert!(self.done, "reaction was not executed");
    println!("success");
  =}
}

main reactor {
  source = new Source()
  sink = new Sink()

  source.out -> sink.in
}
