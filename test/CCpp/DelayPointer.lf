// This is "polymorphic" delay reactor.
// It delays by a logical time any pointer datatype.
target CCpp;
reactor DelayPointer(delay:time(100 msec)) {
    input in:void*;
    output out:void*;
    logical action a:void*;
    reaction(a) -> out {=
        // Using SET_TOKEN delegates responsibility for
        // freeing the allocated memory downstream.
        SET_TOKEN(out, a->token);
    =}
    reaction(in) -> a {=
        // Schedule the actual token from the input rather than
        // a new token with a copy of the input value.
        schedule_token(a, self->delay, in->token);
    =}
}
reactor Source {
    output out:int*;
    reaction(startup) -> out {=
        SET_NEW(out);
        *(out->value) = 42;
    =}
}
reactor Test {
    input in:int*;
    state start_time:time(0);
    state received_value:bool(false);
    reaction(startup) {=
        // Record the logical time at the start.
        self->start_time = get_logical_time();
    =} 
    reaction(in) {=
        printf("Received: %d.\n", *(in->value));
        self->received_value = true;
        // Check the time of the input.
        instant_t current_time = get_logical_time();
        interval_t elapsed = current_time - self->start_time;
        printf("After %lld nsec of logical time.\n", elapsed);
        if (elapsed != 100000000LL) {
            printf("ERROR: Expected elapsed time to be 100000000. It was %lld.\n", elapsed);
            exit(1);
        }
        if (*(in->value) != 42) {
            printf("ERROR: Expected input value to be 42. It was %d.\n", *(in->value));
            exit(2);            
        }
    =}
    reaction(shutdown) {=
        printf("Checking that communication occurred.\n");
        if (!self->received_value) {
            printf("ERROR: No communication occurred!\n");
            exit(3);            
        }
    =}
}

main reactor DelayPointerTest { 
    s = new Source();
    d = new DelayPointer();
    t = new Test();
    s.out -> d.in;
    d.out -> t.in; 
}