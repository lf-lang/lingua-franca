# Check multiport output to bank of recipients. Here, the bank is smaller than the width of the
# sending port.
target Python {
  timeout: 2 sec,
  fast: true
}

import Source from "MultiportToBank.lf"

reactor Destination(bank_index=0) {
  input inp
  state received = False

  reaction(inp) {=
    print("Destination {:d} received {:d}.\n".format(self.bank_index, inp.value))
    if self.bank_index != inp.value:
      sys.stderr.write("ERROR: Expected {:d}.\n".format(self.bank_index))
      exit(1)
    self.received = True
  =}

  reaction(shutdown) {=
    if self.received is not True:
      sys.stderr.write("ERROR: Destination {:d} received no input!\n".format(self.bank_index))
      exit(1)
    print("Success.")
  =}
}

reactor Container {
  input[3] inp
  c = new[3] Destination()
  inp -> c.inp
}

main reactor MultiportToBankHierarchy {
  a = new Source()
  b = new Container()
  a.out -> b.inp
}
