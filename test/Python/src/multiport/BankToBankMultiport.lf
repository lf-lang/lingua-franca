# Check bank of reactors sending to bank of reactors with multiports.
target Python {
  timeout: 2 sec,
  fast: true
}

reactor Source(width=1) {
  timer t(0, 200 msec)
  output[width] out
  state s = 0

  reaction(t) -> out {=
    for port in out:
      port.set(self.s)
      self.s += 1
  =}
}

reactor Destination(width=1) {
  state s = 6
  input[width] _in

  reaction(_in) {=
    sm = 0
    for port in _in:
      if port.is_present:
        sm += port.value

    print("Sum of received: ", sm)
    if sm != self.s:
      sys.stderr.write("ERROR: Expected {:d}.\n".format(self.s))
      exit(1)

    self.s += 16
  =}

  reaction(shutdown) {=
    if self.s <= 6:
      sys.stderr.write("ERROR: Destination received no input!\n")
      exit(1)

    print("Success.")
  =}
}

main reactor BankToBankMultiport(bank_width=4) {
  a = new[bank_width] Source(width=4)
  b = new[bank_width] Destination(width=4)
  a.out -> b._in
}
