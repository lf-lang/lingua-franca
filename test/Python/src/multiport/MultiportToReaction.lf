# Check reaction to multiport output of a contained reactor.
target Python {
  timeout: 2 sec,
  fast: true
}

reactor Source(width=1) {
  timer t(0, 200 msec)
  state s = 0
  output[width] out

  reaction(t) -> out {=
    for port in out:
      port.set(self.s)
      self.s += 1
  =}
}

main reactor {
  state s = 6
  b = new Source(width=4)

  reaction(b.out) {=
    sm = 0
    for port in b.out:
      if port.is_present:
        sm += port.value
    print("Sum of received: ", sm)
    if sm != self.s:
      sys.stderr.write("ERROR: Expected {:d}.\n".format(self.s))
      exit(1)
    self.s += 16
  =}

  reaction(shutdown) {=
    if self.s <= 6:
      sys.stderr.write("ERROR: Destination received no input!\n")
      exit(1)
    print("Success.")
  =}
}
