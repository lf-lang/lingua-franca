# Check multiport output to bank of recipients. Here, the bank is smaller than the width of the
# sending port.
target Python {
  timeout: 2 sec,
  fast: true
}

reactor Source(check_override=0, bank_index=0) {
  output out

  reaction(startup) -> out {= out.set(self.bank_index * self.check_override) =}
}

reactor Destination {
  input[3] _in
  state received = 0

  reaction(_in) {=
    for (idx, port) in enumerate(_in):
      print("Destination channel " + str(idx) + " received " + str(port.value))
      if idx != port.value:
        sys.stderr.write("ERROR: Expected " + str(idx))
        exit(1)

    self.received = True
  =}

  reaction(shutdown) {=
    if self.received is False:
      sys.stderr.write("ERROR: Destination received no input!\n")
      exit(1)

    print("Success.")
  =}
}

main reactor MultiportFromBank {
  a = new[3] Source(check_override=1)
  b = new Destination()
  a.out -> b._in
}
