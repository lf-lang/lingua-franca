# This is a version of the Threaded test that uses a multiport input at the destination. Its purpose
# is to test multiport inputs.
target Python {
  timeout: 2 sec,
  fast: true
}

reactor Source {
  timer t(0, 200 msec)
  output out
  state s = 0

  reaction(t) -> out {=
    out.set(self.s)
    self.s += 1
  =}
}

reactor Computation {
  input _in
  output out

  reaction(_in) -> out {= out.set(_in.value) =}
}

reactor Destination(width=1) {
  state s = 0
  input[width] _in

  reaction(_in) {=
    sm = 0
    for port in _in:
      sm += port.value
    print("Sum of received: ", sm)
    if sm != self.s:
      sys.stderr.write("ERROR: Expected {:d}.\n".format(self.s))
      exit(1)
    self.s += 4
  =}

  reaction(shutdown) {=
    if self.s == 0:
      sys.stderr.write("ERROR: Destination received no input!\n")
      exit(1)
    print("Success.");
  =}
}

main reactor MultiportInParameterized {
  a = new Source()
  t1 = new Computation()
  t2 = new Computation()
  t3 = new Computation()
  t4 = new Computation()
  b = new Destination(width=4)
  a.out -> t1._in
  a.out -> t2._in
  a.out -> t3._in
  a.out -> t4._in
  # I.e.: t1.out -> b._in[0]; t2.out -> b._in[1]; t3.out -> b._in[2]; dt4.out
  # -> b._in[3];
  t1.out, t2.out, t3.out, t4.out -> b._in
}
