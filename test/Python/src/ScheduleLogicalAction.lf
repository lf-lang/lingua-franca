# This checks that a logical action is scheduled the specified logical time after the current
# logical time.
target Python {
  fast: true,
  timeout: 3 sec
}

reactor foo {
  input x
  output y
  logical action a

  reaction(x) -> y, a {=
    y.set(2*x.value)
    # The following uses physical time, incorrectly.
    a.schedule(MSEC(500))
  =}

  reaction(a) -> y {= y.set(-42) =}
}

reactor print {
  state expected_time = 0
  input x

  reaction(x) {=
    elapsed_time = lf.time.logical_elapsed()
    print("Result is ", x.value)
    print("Current logical time is: ", elapsed_time)
    print("Current physical time is: ", lf.time.physical_elapsed())
    if elapsed_time != self.expected_time:
      sys.stderr.write("ERROR: Expected logical time to be {:d}.\n".format(self.expected_time))
      exit(1);
    self.expected_time += MSEC(500)
  =}
}

main reactor {
  f = new foo()
  p = new print()
  timer t(0, 1 sec)
  f.y -> p.x

  reaction(t) -> f.x {= f.x.set(42) =}
}
