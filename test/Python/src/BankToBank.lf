# Check bank of reactors sending to bank of reactors.
target Python {
  timeout: 2 sec,
  fast: true
}

reactor Source(bank_index=0) {
  timer t(0, 200 msec)
  output out
  state s = 0

  reaction(t) -> out {=
    out.set(self.s)
    self.s += self.bank_index
  =}
}

reactor Destination(bank_index=0) {
  state s = 0
  input inp

  reaction(inp) {=
    print("Destination " + str(self.bank_index) + " received: " + str(inp.value))
    if (inp.value != self.s):
      sys.stderr.write("ERROR: Expected " + str(self.s))
      exit(1)
    self.s += self.bank_index
  =}

  reaction(shutdown) {=
    if self.s == 0 and self.bank_index != 0:
      sys.stderr.write("ERROR: Destination " + self.bank_index + " received no input!")
      exit(1)
    print("Success.")
  =}
}

main reactor BankToBank(width=4) {
  a = new[width] Source()
  b = new[width] Destination()
  a.out -> b.inp
}
