# Test data transport across hierarchy.
target Python {
  timeout: 5 sec,
  fast: true
}

reactor Source {
  output out
  timer t(0, 1 sec)

  reaction(t) -> out {=
    out.set(1)
  =}
}

reactor Count {
  output out
  timer t(0, 1 sec)
  state i = 0

  reaction(t) -> out {=
    self.i += 1
    out.set(self.i)
  =}
}

reactor Add {
  input in1
  input in2
  output out

  reaction(in1, in2) -> out {=
    result = 0
    if in1.is_present:
      result += in1.value
    if in2.is_present:
      result += in2.value
    out.set(result)
  =}
}

reactor Print {
  input inp
  state expected = 2

  reaction(inp) {=
    print("Received: ", inp.value)
    if inp.value != self.expected:
      sys.stderr.write("Expected {:d}.\n".format(self.expected))
      exit(1)
    self.expected+=1
  =}
}

reactor AddCount {
  input inp
  output out
  count = new Count()
  add = new Add()
  inp -> add.in1
  count.out -> add.in2
  add.out -> out
}

main reactor Hierarchy2 {
  source = new Source()
  addCount = new AddCount()
  print = new Print()
  source.out -> addCount.inp
  addCount.out -> print.inp
}
