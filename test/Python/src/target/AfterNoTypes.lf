# This test demonstrates that after can be used on connections with ports that have no types. The
# test passes if the compile is succeeded
target Python {
  fast: false,
  timeout: 3 sec
}

reactor Foo {
  input x
  output y

  reaction(x) -> y {= y.set(2*x.value); =}
}

reactor Print {
  state expected_time = 10 msec
  state received = 0
  input x

  reaction(x) {=
    self.received+=1
    elapsed_time = lf.time.logical_elapsed()
    print("Result is " + str(x.value))
    if x.value != 84:
      sys.stderr.write("ERROR: Expected result to be 84.\n")
      exit(1)

    print("Current logical time is: " + str(elapsed_time))
    print("Current physical time is: " + str(lf.time.physical_elapsed()))
    if elapsed_time != self.expected_time:
      sys.stderr.write("ERROR: Expected logical time to be " + self.expected_time)
      exit(2)
    self.expected_time += SEC(1)
  =}

  reaction(shutdown) {=
    if (self.received == 0):
      sys.stderr.write("ERROR: Final reactor received no data.\n")
      exit(3)
  =}
}

main reactor {
  f = new Foo()
  p = new Print()
  timer t(0, 1 sec)
  f.y -> p.x after 10 msec

  reaction(t) -> f.x {= f.x.set(42) =}
}
