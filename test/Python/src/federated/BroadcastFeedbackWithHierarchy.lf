/** This tests an output that is broadcast back to a multiport input of a bank. */
target Python {
  timeout: 1 sec
}

reactor SenderAndReceiver {
  output out
  input[2] in_
  state received = False

  r = new Receiver()
  in_ -> r.in_

  reaction(startup) -> out {= out.set(42) =}
}

reactor Receiver {
  preamble {= import sys =}
  input[2] in_
  state received = False

  reaction(in_) {=
    if in_[0].is_present and in_[1].is_present and in_[0].value == 42 and in_[1].value == 42:
      print("SUCCESS")
      self.received = True
  =}

  reaction(shutdown) {=
    if not self.received:
      print("Failed to receive broadcast")
      self.sys.exit(1)
  =}
}

federated reactor {
  s = new[2] SenderAndReceiver()
  (s.out)+ -> s.in_
}
