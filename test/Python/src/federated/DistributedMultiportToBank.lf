# Check multiport to bank connections between federates.
target Python {
  timeout: 1 sec
}

reactor Source {
  output[2] out
  timer t(0, 100 msec)
  state count = 0

  reaction(t) -> out {=
    for i in range(len(out)):
      out[i].set(self.count)
    self.count += 1
  =}
}

reactor Destination {
  preamble {=
    import sys
  =}
  input inp
  state count = 0

  reaction(inp) {=
    print("Received {}.".format(inp.value))
    if self.count != inp.value:
      self.sys.stderr.write("Expected {}.\n".format(self.count))
      self.sys.exit(1)
    self.count += 1
  =}

  reaction(shutdown) {=
    if self.count == 0:
      self.sys.stderr.write("No data received.")
      self.sys.exit(1)
  =}
}

federated reactor DistributedMultiportToBank {
  s = new Source()
  d = new[2] Destination()
  s.out -> d.inp
}
