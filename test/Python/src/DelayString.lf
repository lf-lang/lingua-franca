# This tests actions with immutable payloads that are neither malloc'd nor freed.
target Python

reactor DelayString2(delay = 100 msec) {
  input inp
  output out
  logical action a

  reaction(a) -> out {=
    out.set(a.value)
  =}

  reaction(inp) -> a {=
    a.schedule(self.delay, inp.value)
  =}
}

reactor Test {
  input inp
  state start_time = 0

  reaction(inp) {=
    print("Received: ", inp.value)
    # Check the time of the input.
    elapsed = lf.time.logical_elapsed()
    print("After {:d} nsec of logical time.\n".format(elapsed))
    if elapsed != 100000000:
      sys.stderr.write("ERROR: Expected elapsed time to be 100000000. It was {:d}.\n".format(elapsed))
      exit(1)
    if inp.value != "Hello":
      sys.stderr.write("ERROR: Expected input value to be 'Hello'. It was '{:s}'.\n".format(inp.value))
      exit(2)
  =}
}

main reactor {
  d = new DelayString2()
  t = new Test()
  d.out -> t.inp

  reaction(startup) -> d.inp {=
    d.inp.set("Hello")
  =}
}
