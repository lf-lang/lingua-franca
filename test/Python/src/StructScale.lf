# Source produces a dynamically allocated class object, which it passes to Scale. Scale modifies it
# and passes it to Print.
target Python {
  files: ["include/hello.py"]
}

preamble {=
  import hello
=}

reactor Source {
  output out

  reaction(startup) -> out {=
    out.set(hello.hello("Earth", 42))
  =}
}

# expected parameter is for testing.
reactor TestInput(expected=42) {
  input inp

  reaction(inp) {=
    print("Received: name = {:s}, value = {:d}\n".format(inp.value.name, inp.value.value))
    if inp.value.value != self.expected:
      sys.stderr.write("ERROR: Expected value to be {:d}.\n".format(self.expected))
      exit(1)
  =}
}

reactor Print(scale=2) {
  # Mutable keyword indicates that this reactor wants a writable copy of the input.
  mutable input inp

  output out

  reaction(inp) -> out {=
    inp.value.value *= self.scale;
    out.set(inp.value)
  =}
}

main reactor StructScale {
  s = new Source()
  c = new Print()
  p = new TestInput(expected=84)
  s.out -> c.inp
  c.out -> p.inp
}
