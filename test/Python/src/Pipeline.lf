target Python {
  timeout: 2 sec
}

reactor TakeTime {
  input inp
  output out

  reaction(inp) -> out {=
    offset = 0
    for i in range(10000):
      offset+=1

    out.set(inp.value + offset)
  =}
}

reactor Print {
  input inp
  state count = 0
  state received = 0

  reaction(inp) {=
    self.received += 1
    print("Received: {:d} at logical time {:d}".format(inp.value, lf.time.logical_elapsed()))
    if inp.value != (self.count + 40000):
      sys.stderr.write("ERROR: Expected {:d}.\n".format(self.count + 40000))
      exit(1)
    self.count+=1
  =}

  reaction(shutdown) {=
    if self.received == 0:
      sys.stderr.write("ERROR: Final reactor received no data.\n")
      exit(3)
  =}
}

main reactor Pipeline {
  timer t(0, 200 msec)
  state count = 0

  c1 = new TakeTime()
  c2 = new TakeTime()
  c3 = new TakeTime()
  c4 = new TakeTime()
  p = new Print()

  c1.out -> c2.inp after 200 msec
  c2.out -> c3.inp after 200 msec
  c3.out -> c4.inp after 200 msec
  c4.out -> p.inp

  reaction(t) -> c1.inp {=
    c1.inp.set(self.count)
    self.count += 1
  =}
}
