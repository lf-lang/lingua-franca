# Check multiport output to bank of recipients.
target Python {
  timeout: 2 sec,
  fast: true
}

reactor Source {
  output[3] out

  reaction(startup) -> out {=
    for (idx, port) in enumerate(out):
      port.set(idx)
  =}
}

reactor Destination(bank_index=0) {
  input inp
  state received = 0

  reaction(inp) {=
    print("Destination " + str(self.bank_index) + " received " + str(inp.value))
    if self.bank_index != inp.value:
      sys.stderr.write("ERROR: Expected " + str(self.bank_index))
      exit(1)
    self.received = True
  =}

  reaction(shutdown) {=
    if self.received is not True:
      sys.stderr.write("ERROR: Destination " + str(self.bank_index) + " received no input!\n")
      exit(1)
    print("Success.")
  =}
}

main reactor MultiportToBank {
  a = new Source()
  b = new[3] Destination()
  a.out -> b.inp
}
