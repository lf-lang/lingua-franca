# Test unconnected input.
target Python {
  timeout: 5 sec,
  fast: true
}

reactor Source {
  output out
  timer t(0, 1 sec)
  state s = 1

  reaction(t) -> out {=
    out.set(self.s)
    self.s += 1
  =}
}

reactor Add {
  input in1
  input in2
  output out

  reaction(in1, in2) -> out {=
    result = 0
    if in1.is_present:
      result += in1.value
    if in2.is_present:
      result += in2.value
    out.set(result)
  =}
}

reactor Print {
  input inp
  state expected = 1

  reaction(inp) {=
    print("Received: ", inp.value)
    if inp.value != self.expected:
      sys.stderr.write("ERROR: Expected {:d}.\n".format(self.expected))
      exit(1)

    self.expected +=1
  =}
}

main reactor UnconnectedInput {
  source = new Source()
  add = new Add()
  print = new Print()
  source.out -> add.in2
  add.out -> print.inp
}
