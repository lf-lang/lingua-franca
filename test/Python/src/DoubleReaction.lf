# Test that two simultaneous inputs that trigger a reaction trigger it only once. Correct output for
# this 2, 4, 6, 8, etc.
target Python {
  timeout: 10 sec,
  fast: true
}

reactor Clock(offset=0, period = 1 sec) {
  output y
  timer t(offset, period)
  state count = 0

  reaction(t) -> y {=
    self.count += 1
    y.set(self.count)
  =}
}

reactor Destination {
  input x
  input w
  state s = 2

  reaction(x, w) {=
    sm = 0
    if x.is_present:
      sm += x.value
    if w.is_present:
      sm += w.value
    print("Sum of inputs is: ", sm)
    if sm != self.s:
      sys.stderr.write("FAILURE: Expected sum to be {:d}, but it was {:d}.\n".format(self.s, sm));
      exit(1)
    self.s += 2
  =}
}

main reactor DoubleReaction {
  c1 = new Clock()
  c2 = new Clock()
  d = new Destination()
  c1.y -> d.x
  c2.y -> d.w
}
