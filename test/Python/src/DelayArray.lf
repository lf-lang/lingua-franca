# This tests delaying an array type.
target Python

reactor DelayPointer(delay = 100 msec) {
  # The Python target does not require explicit type allocation for types other than time
  mutable input inp
  output out
  logical action a

  reaction(inp) -> a {=
    # mutable input guarantees in will not be freed.
    a.schedule(self.delay, inp.value);
  =}

  reaction(a) -> out {=
    out.set(a.value);
  =}
}

reactor Source {
  output out

  reaction(startup) -> out {=
    # Send an output list of length 3.
    out.set([0, 1, 2])
  =}
}

# The scale parameter is just for testing.
reactor Print(scale=1) {
  input inp

  reaction(inp) {=
    print("Received: [%s]" % ", ".join(map(str, inp.value)))
    if inp.value != [x * self.scale for x in [0, 1, 2]]:
      sys.stderr.write("ERROR: Value received by Print does not match expectation!\n")
      exit(1)
  =}
}

main reactor DelayArray {
  s = new Source()
  d = new DelayPointer()
  p = new Print()
  s.out -> d.inp
  d.out -> p.inp
}
