target Python

reactor Super {
  state count = 0

  reaction(startup) {=
    print("{:s}(Super) started".format(self.name))
    self.count += 1
  =}

  reaction(shutdown) {=
    if self.count == 0:
      sys.stderr.write("No startup reaction was invoked!\n")
      exit(3)
  =}
}

reactor SubA(name="SubA") extends Super {
  reaction(startup) {=
    print("{:s} started".format(self.name))
    if self.count == 0:
      sys.stderr.write("Base class startup reaction was not invoked!\n")
      exit(1)
  =}
}

reactor SubB(name="SubB") extends Super {
  reaction(startup) {=
    print("{:s} started".format(self.name))
    if self.count == 0:
      sys.stderr.write("Base class startup reaction was not invoked!\n")
      exit(2)
  =}
}

main reactor SubclassesAndStartup {
  a = new SubA()
  b = new SubB()
}
