# Tests the lf.tag_compare() function in the python target.
target Python {
  timeout: 10 msec,
  fast: false
}

main reactor CompareTags {
  preamble {= import sys =}
  timer t(0, 1 msec)
  logical action l

  reaction(t) -> l {=
    tag1 = lf.tag()
    tag2 = lf.tag()
    if (lf.tag_compare(tag1, tag2) != 0 or not tag1 == tag2 or tag1 != tag2):
      self.sys.stderr.write("Tags should be equal\n")
      self.sys.exit(1)
    l.schedule(0, tag1)
  =}

  reaction(l) {=
    tag3 = lf.tag()
    tag1 = l.value
    if (lf.tag_compare(tag1, tag3) != -1 or not tag1 < tag3 or tag1 >= tag3):
      self.sys.stderr.write("tag1 should be lesser than tag3\n")
      self.sys.exit(1)
    if (lf.tag_compare(tag3, tag1) != 1 or not tag3 > tag1 or tag3 <= tag1):
      self.sys.stderr.write("tag3 should be greater than tag1\n")
      self.sys.exit(1)
  =}
}
