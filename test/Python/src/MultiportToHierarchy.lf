# Check multiport output to multiport input, where the latter is a hierarchical reactor. Note that
# the destination reactor has width wider than the sender, so one input is dangling.
target Python {
  timeout: 2 sec,
  fast: true
}

reactor Source {
  timer t(0, 200 msec)
  output[4] out
  state s = 0

  reaction(t) -> out {=
    for port in out:
      port.set(self.s)
      self.s += 1
  =}
}

reactor Destination(width=4) {
  state s = 6
  input[width] inp

  reaction(inp) {=
    sm = 0
    for port in inp:
      if port.is_present:
        sm += port.value
    print("Sum of received: ", sm)
    if sm != self.s:
      sys.stderr.write("ERROR: Expected {:d}.\n".format(self.s))
      exit(1)
    self.s += 16
  =}

  reaction(shutdown) {=
    if self.s <= 6:
      sys.stderr.write("ERROR: Destination received no input!\n")
      exit(1)
    print("Success.")
  =}
}

reactor Container(width=4) {
  input[width] inp
  dst = new Destination()
  inp -> dst.inp
}

main reactor MultiportToHierarchy {
  a = new Source()
  b = new Container()
  a.out -> b.inp
}
