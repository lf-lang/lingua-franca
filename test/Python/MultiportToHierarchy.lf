 // Check multiport output to multiport input, where the latter is a hierarchical reactor.
 // Note that the destination reactor has width wider than the sender, so one input is dangling.
target Python {
    timeout: 2 sec,
    threads: 4,
    fast: true
}; 
reactor Source {
	timer t(0, 200 msec);
	output[4] out:int;
	state s:int(0);
	reaction(t) -> out {=
        for i in range(len(out)):
            out[i].set(self.s)
            self.s += 1
	=}
}
reactor Destination(width:int(4)) {
	state s:int(6);
	input[width] _in:int;
	reaction(_in) {=
        sm = 0
        for i in range(len(_in)):
            if _in[i].is_present:
                sm += _in[i].value
        print("Sum of received: ", sm)
        if sm != self.s:
            sys.stderr.write("ERROR: Expected {:d}.\n".format(self.s))
            exit(1)
        self.s += 16
    =}
    reaction(shutdown) {=
        if self.s <= 6:
            sys.stderr.write("ERROR: Destination received no input!\n")
            exit(1)
        print("Success.")
    =}
}
reactor Container(width:int(4)) {
    input[width] in:int;
    dst = new Destination();
    in -> dst._in;
}

main reactor MultiportToHierarchy  { 
	a = new Source();
	b = new Container();
	a.out -> b.in;
}