// Check reaction to multiport output of a contained reactor.
target Python {
    timeout: 2 sec,
    threads: 4,
    fast: true
}; 
reactor Source(width:int(1)) {
    timer t(0, 200 msec);
    state s:int(0);
    output[width] out:int;
    reaction(t) -> out {=
        for i in range(len(out)):
            out[i].set(self.s)
            self.s += 1
    =}
}
main reactor MultiportFromReaction {
    state s:int(6);
    reaction(b.out) {=
        sm = 0
        for i in range(len(b.out)):
            if b.out[i].is_present:
                sm += b.out[i].value
        print("Sum of received: ", sm)
        if sm != self.s:
            sys.stderr.write("ERROR: Expected {:d}.\n".format(self.s))
            exit(1)
        self.s += 16
    =}
    reaction(shutdown) {=
        if self.s <= 6:
            sys.stderr.write("ERROR: Destination received no input!\n")
            exit(1)
        print("Success.")
    =}
    b = new Source(width = 4);
}
