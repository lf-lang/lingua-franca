target Python {
    files: [
        "ROS/carla_intersection/src/carla_sim.py",
        "ROS/carla_intersection/src/utils.py"
    ]
};

preamble {=
    from utils import make_coordinate, make_spawn_point
    from carla_sim import CarlaSim
=}

# TODO: Figure out why this warning persists |
#                                            v
# WARNING: World::ApplySettings: After 30  attemps, the settings were not correctly set. Please check that everything is consistent.
#
# as well as why the velocity of the vehicle shoots through the roof... which causes CARLA to crash. 
reactor CarlaSim(
    interval(16 msec),
    bank_index(0),
    initial_velocities({=None=}),
    vehicle_type("vehicle.tesla.model3"),
    spawn_points({=None=})
) {
    input command_;
    output velocity_;
    output position_;
    logical action world_is_ready;
    state carla_sim;
    timer timer_(10 msec, interval);

    preamble {=
        class Logger:
            def __init__(self, vehicle_id):
                self.vehicle_id = vehicle_id

            def info(self, *args):
                print(f"{get_logical_time()} - carla_sim_{self.vehicle_id}: ", args)
    =}

    reaction(startup) -> world_is_ready {=
        print("initial velocity: ", self.initial_velocities[self.bank_index])
        self.carla_sim = CarlaSim(interval=self.interval, 
                                  vehicle_type=self.vehicle_type, 
                                  initial_velocity=make_coordinate(self.initial_velocities[self.bank_index]), 
                                  spawn_point=make_spawn_point(self.spawn_points[self.bank_index]),
                                  logger=self.Logger(self.bank_index))
        self.carla_sim.connect_to_carla()
        if self.bank_index == 0:
            self.carla_sim.initialize_world()
        world_is_ready.schedule(MSEC(5))
    =}

    reaction(world_is_ready) {=
        self.carla_sim.get_world()
        self.carla_sim.initialize_vehicle()
    =}

    reaction(timer_) -> velocity_, position_ {=
        self.carla_sim.tick()
        print("setting velocity to ", self.carla_sim.get_vehicle_velocity())
        velocity_.set(self.carla_sim.get_vehicle_velocity())
        p = self.carla_sim.get_vehicle_position()
        coordinate = make_coordinate([p.latitude, p.longitude, p.altitude])
        position_.set(coordinate)
    =}

    reaction(command_) {=
        cmd = command_.value
        self.carla_sim.apply_control(cmd.throttle, cmd.brake)
    =}
}