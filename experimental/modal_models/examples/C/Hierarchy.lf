target C;

import Converter from "ConvertCase.lf";
import PrintChar from "lib/Printer.lf";
import TimedSeparator, TimedStep from "lib/Timer.lf";

reactor Processor {
    input pause:bool;
	input char_in:char;
    output char_out:char;
    
    initial mode Wait {
		reaction(char_in) {=
            printf("Sleeping (discarded: %c)\n", char_in->value);
		=}

		reaction(pause) -> Process reset {=
            printf("Resuming (reset)\n");
            SET_MODE(Process);
		=}
    }
    
    mode Process {
		converter = new Converter(log=true);
		char_in -> converter.raw;
		converter.converted -> char_out;
		
		reaction(pause) -> Wait {=
            printf("Pausing\n");
            SET_MODE(Wait);
		=}
    }
}

main reactor Hierarchy {
    step = new TimedStep();
    chars = new TimedSeparator(offset=250msec, period=250msec,message="AaB xxxxAaBbxxxx AaB");
    processor = new Processor();
    printer = new PrintChar(prefix="Result: ");
    
    step.step -> processor.pause;
    chars.character -> processor.char_in;
    processor.char_out -> printer.character;
    
    preamble {=
    	#include <stdio.h>
    =}
    
    reaction(startup) {=
        printf("Start\n");
    =}
}
