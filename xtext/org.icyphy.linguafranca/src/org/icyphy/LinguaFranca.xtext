/* The Lingua Franca grammar. */

/*************
Copyright (c) 2020, The University of California at Berkeley.

Redistribution and use in source and binary forms, with or without modification,
are permitted provided that the following conditions are met:

1. Redistributions of source code must retain the above copyright notice,
   this list of conditions and the following disclaimer.

2. Redistributions in binary form must reproduce the above copyright notice,
   this list of conditions and the following disclaimer in the documentation
   and/or other materials provided with the distribution.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND 
ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED 
WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE 
DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR
ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES 
(INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; 
LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON 
ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT 
(INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS 
SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
***************/

/** 
 * Grammar for Lingua Franca.
 * A note of caution: extending this grammar with productions that introduce
 * new terminals (i.e., anything written between quotes), will require those 
 * terminals to also be added to the Token production at the bottom of this
 * file. Failing to do so will cause a parse error whenever a terminal not
 * listed in the Token production is featured in a segment of target-language
 * code (e.g., a reaction body).
 * @author{Marten Lohstroh <marten@berkeley.edu>}
 * @author{Edward A. Lee <eal@berkeley.edu>}
 */

grammar org.icyphy.LinguaFranca with org.eclipse.xtext.common.Terminals

generate linguaFranca "http://www.icyphy.org/LinguaFranca"

/////////// Overall file

/**
 * Top-level AST node.
 */
Model:
    target=Target
    (imports+=Import)*
    (preambles+=Preamble)*
    (reactors+=Reactor)+
    ;

/////////// Top level elements

/**
 * Import declaration.
 */
Import:
    'import' importURI=Path ';'
    ;

/**
 * Declaration of a reactor class.
 */
Reactor:
    ((federated?='federated' | main?='main')? & realtime?='realtime'?) 'reactor' name=ID 
    ('<' typeParms+=TypeParm (',' typeParms+=TypeParm)* '>')?
    ('(' parameters+=Parameter (',' parameters+=Parameter)* ')')?
    ('at' host=Host)?
    ('extends' (superClasses+=[Reactor] (',' superClasses+=[Reactor])*))?
    '{'
    (     (preambles+=Preamble)
        | (stateVars+=StateVar)
        | (inputs+=Input)
        | (outputs+=Output)
        | (timers+=Timer)
        | (actions+=Action)
        | (instantiations+=Instantiation)
        | (connections+=Connection)
        | (reactions+=Reaction)
        | (mutations+=Mutation)
    )* '}';


TypeParm:
    literal=TypeExpr | code=Code
;

// Allows simple statements like "A extends B". We probably want to further expand this.
TypeExpr:
    ID+
;

/**
 * Specification of the target language. Target properties can be specified in
 * YAML format to pass on configuration details to the runtime environment. 
 */
Target:
    'target' name=ID (config=KeyValuePairs)? ';';


/////////// Statements

/**
 * Declaration of a state variable. Types are optional, but may be required 
 * during validation (depending on the target language). Initialization is also 
 * optional. A state variable can be initialized by assigning a `Value` or list 
 * of these. Note that a `Value` may also be a reference to a parameter.
 * The following checks must be carried out during validation: 
 *  - if the list of initialization values has more than one element in it, a 
 *  type must be specified;
 *  - if the `time` type is specified, there can only be a single initialization
 *  element, which has to denote a time or a reference to a parameter that
 *  denotes a time; and
 *  - if the `time` type is specified, either a proper time interval and unit
 *  must be given, or a literal or code that denotes zero.
 */
 StateVar:
    'state' name=ID (
        (':' (type=Type))? 
        (parens+='(' (init+=Value 
                (','  init+=Value)*)? parens+=')'
        )?
    ) ';'    
;

Input:
    mutable?='mutable'? 'input' (widthSpec=WidthSpec)? name=ID (':' type=Type)? ';';

Output:
    'output' (widthSpec=WidthSpec)? name=ID (':' type=Type)? ';';

// Timing specification for a timer: (offset, period)
// Can be empty, which means (0,0) = (NOW, ONCE).
// E.g. (0) or (NOW) or (NOW, ONCE) or (100, 1000)
// The latter means fire with period 1000, offset 100.
Timer:
    'timer' name=ID ('(' offset=Value (',' period=Value)? ')')? ';';

Boolean:
    TRUE | FALSE
;

terminal TRUE:
    'true';
    
terminal FALSE:
    'false';

// Action that has either a physical or logical origin.
// 
// If the origin is logical, the minDelay is a minimum logical delay
// after the logical time at which schedule() is called that the
// action will occur. If the origin is physical, then the 
// minDelay is a minimum logical delay after the physical time
// at which schedule() is called that the action will occur.
//
// For all actions, minInterArrival is the minimum physical time
// to elapse between action occurrences.
Action:
    (origin=ActionOrigin)? 'action' name=ID 
    ('(' minDelay=Value (',' minInterArrival=Value (',' (drop?=TRUE | FALSE))? )? ')')?
    (':' type=Type)? ';';

Reaction:
    ('reaction')
    ('(' (triggers+=TriggerRef (',' triggers+=TriggerRef)*)? ')')?
    (sources+=VarRef (',' sources+=VarRef)*)?
    ('->' effects+=VarRef (',' effects+=VarRef)*)?
    code=Code
    (deadline=Deadline)?;

TriggerRef:
    VarRef | startup?='startup' | shutdown?='shutdown';

Deadline:
    'deadline' '(' delay=Value ')' code=Code;

Mutation:
    ('mutation')
    ('(' (triggers+=[Trigger] (',' triggers+=[Trigger])*)? ')')?
    (sources+=[Input] (',' sources+=[Input])*)?
    ('->' effects+=[Effect] (',' effects+=[Effect])*)?
    code=Code;

Preamble:
    (visibility=Visibility)? 'preamble' code=Code;

Instantiation:
    name=ID '=' 'new' (arraySpec=ArraySpec)?
    reactorClass=[Reactor] ('<' typeParms+=TypeParm (',' typeParms+=TypeParm)* '>')? '(' 
    (parameters+=Assignment (',' parameters+=Assignment)*)? 
    ')' ('at' host=Host)? ';';

Connection:
    leftPort=VarRef ('->' | physical?='~>') rightPort=VarRef ('after' delay=Value)? ';';
    
/////////// For target parameters

KeyValuePairs:
    '{' pairs+=KeyValuePair (',' (pairs+=KeyValuePair))* '}';

KeyValuePair:
    name=Kebab ':' value=Element;

Array:
    '[' elements+=Element (',' (elements+=Element))* ']';
    
Element:
    keyvalue=KeyValuePairs 
    | array=Array
    | literal=Literal 
    | (time=INT unit=TimeUnit) 
    | id=ID;

///////// Pieces
TypedVariable:
    Port | Action
;

Variable:
    TypedVariable | Timer;

Trigger:
    Action | Input;

Effect:
    Action | Output;

VarRef:
    variable=[Variable] (variableArraySpec=ArraySpec)?
    | container=[Instantiation] (containerArraySpec=ArraySpec)? '.' variable=[Variable] (variableArraySpec=ArraySpec)?;

Assignment:
    (lhs=[Parameter] (('=' rhs+=Value) | ( '='? '(' (rhs+=Value(','  rhs+=Value)*)? ')')));

/**
 * Parameter declaration with optional type and mandatory initialization.
 */
Parameter:
    name=ID (':' (type=Type))? 
    (parens+='(' (init+=Value 
            (','  init+=Value)*)? parens+=')'
    )?
;

// Time is either a reference to a parameter or an integer value, 
// a number followed by a unit specification (unless the value is zero). 
// If it is a constant, the validator should check that if the value
// is non-zero, it is accompanied by a unit.
Value:
    (parameter=[Parameter] | time=Time | literal=Literal | code=Code);


Time:
    (interval=INT unit=TimeUnit)
;

Port:
    Input | Output;
    
// A type is in the target language, hence either an ID or target code.
Type:
   (time?='time' (arraySpec=ArraySpec)?) | ((id=(DottedName) (stars+='*')* ('<' typeParms+=TypeParm (',' typeParms+=TypeParm)* '>')? (arraySpec=ArraySpec)?) | code=Code);

ArraySpec:
    ofVariableLength?='[]' | '[' length=INT ']';
    
WidthSpec:
    ofVariableLength?='[]' | '[' (terms+=WidthTerm) ('+' terms+=WidthTerm)* ']';
    
WidthTerm:
    width=INT | parameter=[Parameter];

IPV4Host:
    (user=Kebab '@')? addr=IPV4Addr (':' port=INT)? // ('/' dir=Directory)?
;

IPV6Host:
    ('[' (user=Kebab '@')? addr=IPV6Addr ']' (':' port=INT)?) // ('/' dir=Directory)?
;

NamedHost:
    (user=Kebab '@')? addr=HostName (':' port=INT)? // ('/' dir=Directory)?
;

Host:
    IPV4Host | IPV6Host | NamedHost
;

HostName:
    (Kebab (('.') Kebab)*)
;

DottedName:
    (ID (('.'|'::') ID)*)
;


Generic:
    '<' (DottedName (',' DottedName)*) '>'
;

SignedInt:
    INT | NEGINT
;

Literal:
    STRING | SignedFloat | SignedInt | Boolean;

/////////// Elementary components
// Terminals must be mutually exclusive. They are used by the lexer before parsing.

@Override
terminal SL_COMMENT: ('//' | '#') !('\n'|'\r')* ('\r'? '\n')?;

@Override
terminal ML_COMMENT: ('/*' -> '*/') | ('\'\'\'' -> '\'\'\'');

Kebab: ID('-'ID)*;

terminal NEGINT:
    '-' ('0'..'9')+;

IPV4Addr:
    INT '.' INT '.' INT '.' INT
;

IPV6Seg:
    // NOTE: This rule is too permissive by design. 
    // Further checking is done during validation.
    (INT | (INT? ID))
;


IPV6Addr:
    // NOTE: This rule is too permissive by design. 
    // Further checking is done during validation. 
    // IPV6 with truncation.
    '::' | ('::' (IPV6Seg (':'))* IPV6Seg) | ((IPV6Seg (':'|'::'))+ IPV6Seg?) | 
    
    // (Link-local IPv6 addresses with zone index) "fe80::7:8%1"
    (ID '::' IPV6Seg (':' IPV6Seg)* '%' (INT | ID)+) |

    // IPv4-mapped IPv6 addresses and IPv4-translated addresses
    ('::' IPV4Addr) | ('::' ID ':' (INT ':')? IPV4Addr) |
    
    // IPv4-Embedded IPv6 Address
    (((IPV6Seg (':' IPV6Seg)* '::') | (IPV6Seg (':' IPV6Seg)*) ':') IPV4Addr)
;


SignedFloat:
    '-'? INT '.' INT;

// FIXME: What if the code needs to contain '=}'?
// Just escaping with \ is not a good idea because then every \ has to be escaped \\.
// Perhaps the string EQUALS_BRACE could become '=}'?
Code:
    //{Code} '{=' (tokens+=Token)* '=}'
    {Code} '{=' body=Body '=}'
;

// The following cannot be terminal because it overlaps ID.
Path: 
    ID ('.' ID)*;
    
// Absolute or relative directory path in Windows, Linux, or MacOS.
Directory:
    (ID ':\\')? ('\\' | '/')? ID (('\\' | '/') ID)*
;

/////////// Enums
enum ActionOrigin:
    NONE | LOGICAL='logical' | PHYSICAL='physical';

enum Visibility:
    NONE | PRIVATE='private' | PUBLIC='public';

enum TimeUnit:
    NONE |
    NSEC='nsec' | NSECS='nsecs' |
    USEC='usec' | USECS='usecs' |
    MSEC='msec' | MSECS='msecs' |
    SEC='sec' | SECS='secs' | SECOND='second' | SECONDS='seconds' |
    MIN='min' | MINS='mins' | MINUTE='minute' | MINUTES='minutes' |
    HOUR='hour' | HOURS='hours' |
    DAY='day' | DAYS='days' |
    WEEK='week' | WEEKS='weeks';

// An arbitrary sequence of terminals.
Body:
    Token*
;
// Production for the tokenization of target code. All terminals used in any
// of the productions in the grammar (except for `{=` and `=}`) have to be
// listed here. Whenever a terminal is encountered amid a sequence of target-
// language tokens that is not featured in this production, this will demarcate
// the end of a target-code segment.
Token:
    // Imported terminals
    ID | INT | STRING | ML_COMMENT | SL_COMMENT | WS | ANY_OTHER | 
    // Keywords
    'target' | 'import' | 'main' | 'realtime' | 'reactor' | 'state' | 'time' | 
    'mutable' | 'input' | 'output' | 'timer' | 'action' | 'reaction' | 
    'startup' | 'shutdown' | 'after' | 'deadline' | 'mutation' | 'preamble' |
    'new' | 'federated' | 'at' |
    // Other terminals
    NEGINT | TRUE | FALSE |
    // Action origins
    'logical' | 'physical' |
    // Visibility modifiers
    'private' | 'public' |
    // Time units
    'nsec' | 'nsecs' | 'usec' | 'usecs' | 'msec' | 'msecs' | 'sec' | 'secs' | 
    'second' | 'seconds' | 'min' | 'mins' | 'minute' | 'minutes' | 'hour' |
    'hours' | 'day' | 'days' | 'week' | 'weeks' |
    // Braces
    '(' | ')' | '{' | '}' |
    // Brackets
    '[' | ']' | '<' | '>' | '[]' |
    // Punctuation
    ':' | ';' | ',' | '.' | '::' |
    // Hyphen
    '-' |
    // Arrow
    '->' | 
    // Assignment
    '=' |
    // Asterisk
    '*' |
    // Sums (for width expressions)
    '+' |
    // Percentage
    '%'
;