// Check for speedup of multithreaded execution on multicore machines.
// Each instance of TakeTime takes 200 ms to transport the input to the output.
// Four of them are instantiated.
// Note that without parallel execution, there is no way this can keep up with real time
// since in every 200 msec cycle it has 800 msec of work to do.
// On a quad-core machine, however, it does pretty well, completing 800 msec of work
// in about 225 msec.
// NOTE: This is the non-threaded version, showing that without threads, this takes more
// than 800 msec to complete 200 msec of logical time.
target Cpp {
    timeout: 2 sec
};

reactor Source {
    timer t(0, 200 msec);
    output out:int;
    state s:int(0);
    reaction(t) -> out {=
        out.set(s);
        s++;
    =}
}
reactor Computation {
    preamble {= 
        #include <thread>
    =}
    input in:int;
    output out:int;
    reaction(in) -> out {=
        std::this_thread::sleep_for(200ms);
        out.set(in.get());
    =}
}
reactor Destination {
    state s:int(0);
    input in1:int;
    input in2:int;
    input in3:int;
    input in4:int;
    reaction(in1, in2, in3, in4) {=
        int sum = *in1.get() + *in2.get() + *in3.get() + *in4.get();
        std::cout << "Sum of received: " << sum << '\n';
        if (sum != s) {
            std::cerr << "ERROR: Expected " << s << '\n';
            exit(1);
        }
        s += 4;
    =}
}

main reactor ScatterGather {
    a = new Source();
    t1 = new Computation();
    t2 = new Computation();
    t3 = new Computation();
    t4 = new Computation();
    b = new Destination();
    a.out -> t1.in;
    a.out -> t2.in;
    a.out -> t3.in;
    a.out -> t4.in;
    t1.out -> b.in1;
    t2.out -> b.in2;
    t3.out -> b.in3;
    t4.out -> b.in4;
}
